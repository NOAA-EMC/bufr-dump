C$$$  MAIN PROGRAM DOCUMENTATION BLOCK
C
C MAIN PROGRAM: PREPOBS_PREPSSMI
C   PRGMMR: DONG             ORG: NP22        DATE: 2020-08-20
C
C ABSTRACT: READS SSM/I SCAN LINES FROM NCEP SSM/I BUFR DUMP FILE
C   (EITHER DERIVED PRODUCTS FILE OR BRIGHTNESS TEMPERATURE FILE) AND
C   UNPACKS 64 RETRIEVALS FROM EACH SCAN.  SUPEROBS ALL DATUM OF THE
C   SAME TYPE (OPTIONAL), AND REPROCESSES EACH UNIQUE RETRIEVAL INTO
C   AN OUTPUT BUFR FILE FOR USE BY PROGRAM PREPOBS_PREPDATA.
C      RETRIEVALS FROM THE INPUT PRODUCTS FILE CAN INCLUDE ANY OR ALL
C   OF: OCEANIC SURFACE WIND SPEED, OCEANIC PRECIPITABLE WATER,
C   RAINFALL RATE, OVERLAND SURFACE TEMPERATURE, OCEANIC SEA-SURFACE
C   TEMPERATURE, OCEANIC CLOUD WATER, SOIL MOISTURE AND SNOW DEPTH
C   WHERE EACH PRODUCT FILLS A UNIQUE REPORT IN OUTPUT FILE; AND
C   POSSIBLY ALSO A SET OF ADDITIONAL PRODUCTS (SURFACE TAG, ICE
C   CONCENTRATION, ICE AGE, ICE EDGE AND CALCULATED SURFACE TYPE)
C   WHICH TOGETHER FILL A REPORT IN OUTPUT FILE.  THIS LATTER SET OF
C   PRODUCTS IS NOT AVAILABLE FOR SUPEROBING.  IF THE INPUT PRODUCTS
C   FILE IS FROM FNOC, ALL OF THE ABOVE PRODUCTS ARE NORMALLY
C   AVAILABLE EXCEPT FOR OCEANIC SEA-SURFACE TEMPERATURE.  IF IT IS
C   AN NCEP PRODUCTS FILE, MOST LIKELY ONLY WIND SPEED, TPW, CLOUD
C   WATER AND SEA-SURFACE TEMPERATURE ALONG WITH SURFACE TAG, WILL BE
C   AVAILABLE.
C      FROM THE INPUT BRIGHTNESS TEMPERATURE FILE, BRIGHTNESS
C   TEMPERATURES FROM SEVEN CHANNELS ARE AVAILABLE IN ADDITION TO
C   WIND SPEED PRODUCT CALCULATED IN-LINE (IN SUBROUTINE W3MISCAN)
C   VIA EITHER THE GOODBERLET ALGORITHM AND/OR THE "LATEST" VERSION
C   OF THE NEURAL NET ALGORITHM, AND OCEANIC PRECIPITABLE WATER
C   PRODUCT CALCULATED IN-LINE (IN SUBROUTINE W3MISCAN) VIA THE SAME
C   NEURAL NET ALGORITHM.
C      FOR INPUT PRODUCTS FILE, THIS PROGRAM CAN SELECT ANY OR ALL
C   COMBINATIONS OF PRODUCTS PROVIDED THEY ARE AVAILABLE (EXCEPT FOR
C   ADDITIONAL PRODUCTS WHERE IT'S ALL OR NONE).  FOR INPUT BRIGHTNESS
C   TEMPERATURE FILE, THE IN-LINE CALCULATED WIND SPEED AND TPW MUST
C   ALWAYS BE FROM THE SAME ALOGIRITHM (I.E., USER CAN'T REQUEST WIND
C   SPEED FROM GOODBERLET ALG. AND TPW FROM LATEST NN ALG.).
C      USER CAN SELECT FROM ANY OR ALL COMBINATIONS OF VALID DMSP
C   SSM/I SATELLITE IDS.  THE OPTIONAL SUPEROBING OF DATUM IS BASED
C   ON A LINEAR AVERAGE OF THE DATUM OVER A USER-SPECIFIED LATITUDE/
C   LONGITUDE BOX.  FOR INPUT FROM BRIGHTNESS TEMPERATURE FILE,
C   IN-LINE PRODUCTS AND BRIGHTNESS TEMPERATURES CANNOT BOTH BE
C   SUPEROED IN A SINGLE RUNNING OF THIS PROGRAM.  OBVIOUSLY, ALSO
C   PRODUCTS FROM INPUT PRODUCTS FILE AND BRIGHTNESS TEMPS FROM
C   INPUT BRIGHTNESS TEMP FILE CAN'T BE SUPEROBED IN A SINGLE
C   RUNNING OF THIS PROGRAM SINCE IT CAN ONLY INPUT ONE OF THE TYPES
C   OF FILES IN A SINGLE RUNNING.
C      USER-SPECIFIED SWITCHES ALLOW RETRIEVALS TO BE SELECTED BY
C   TIME (CENTER TIME AND TIME-WINDOW), BY LOCATION (LAT/LON BOUNDARY
C   FOR DOMAIN), AND BY QUALITY (SEE "NAMELIST" CONTENTS IN COMMENTS
C   FOLLOWING THIS DOCBLOCK).  ALL SUPEROBS STORE NUMBER OF
C   INDIVIDUAL RETRIEVALS THAT WENT INTO MAKING THE SUPEROB.  FOR
C   INDIVIDUAL (NON-SUPEROBED) WIND SPEED PRODUCT RETRIEVALS, THE
C   QUALITY MARK (RAIN FLAG) IS STORED IN THE OUTPUT FILE.  FOR
C   ALL INDIVIDUAL (NON-SUPEROBED) DATA, SATELLITE ID, ORBIT NUMBER,
C   SCAN NUMBER AND POSITION NUMBER ARE STORED IN THE OUTPUT BUFR
C   FILE.
C
C PROGRAM HISTORY LOG:
C 1994-04-12  D. A. KEYSER (NMC22) -- ORIGINAL AUTHOR, REPLACED
C        PREVIOUS VERSION WHICH READ FROM NON-OPERATIONAL NESDIS
C        ORBIT-BY-ORBIT DATA SETS
C 1994-09-06  D. A. KEYSER (NMC22) -- N-LIST SWITCH 'IWINDO' CHANGED
C        FROM WHOLE HOURS TO (HOURS * 100) + MINUTES (FRACTION OF
C        WHOLE HOUR NOW POSSIBLE); IN ORDER TO GET REGION SIZE DOWN
C        BELOW NAS LIMIT OF 9999K BYTES, COMBINED LATITUDE AND
C        LONGITUDE IN NEW STORAGE ARRAY "IPKDTA" (REPLACED "SUMLAT"
C        AND "SUMLON" STORAGE ARRAYS), EACH HELD IN 16-BITS; INCR.
C        NUMBER OF PRODUCTS THAT STORAGE ARRAYS CAN HOLD FROM 2 TO 7
C        TO PREPARE FOR RADIANCE SUPEROBING (AND LOOPING THRU PROGRAM
C        TWICE TO PROCESS PRODUCTS 3 AND 4 REMOVED); MODIFIED TO
C        ALLOW SUPEROBING OVER .5 DEG. BOXES, BUT ONLY IN NORTHERN
C        AND WESTERN HEMISPHERES
C 1994-10-05  D. A. KEYSER (NMC22) -- CODE REDESIGNED TO READ IN AND
C        -IF REQUESTED- SUPEROB SEVEN-CHANNEL BRIGHTNESS TEMPERATURE
C        RETRIEVALS ORIGINATING FROM SDR-TYPE FILES; ADDED THE
C        PRODUCTS CLOUD WATER, SOIL MOISTURE AND SNOW DEPTH TO THE
C        LIST OF PRODUCTS THAT CAN BE PROCESSED AS UNIQUE REPORTS;
C        ADDED A SET OF ADDITIONAL PRODUCTS THAT TOGETHER ARE
C        PROCESSED AS A SINGLE REPORT
C 1995-01-05  D. A. KEYSER (NMC22) -- ADDED ABILITY TO PROCESS AND
C        -IF REQUESTED- SUPEROB WIND SPEED PRODUCTS THAT HAVE BEEN
C        GENERATED IN-LINE (VIA SUBR. W3FI86) USING EITHER THE
C        NEURAL NETWORK ALGORITHM OR THE GOODBERLET ALGORITHM.
C        THE ORIGINAL GOODBERLET WIND SPEED IN THE PRODUCTS DATA
C        SET(S) CAN ALSO STILL BE PROCESSED; NOTE HOWEVER THAT
C        ONLY ONE SOURCE OF WIND SPEED PRODUCT CAN BE CHOSEN IN A
C        SINGLE RUNNING OF THIS PROGRAM
C 1996-07-31  D. A. KEYSER (NP22) -- MODIFIED NAS9000 PREPSSMI PROGRAM
C        TO CALL NEW SUBROUTINE W3MISCAN (INSTEAD OF W3FI86) WHICH
C        READS A SCAN OUT OF THE NCEP SSM/I JBUFR DATA SET.  CAN NOW
C        PROCESS OPERATIONAL FNOC PRODUCTS ALONG WITH IN-LINE
C        CALCULATED WIND SPEED PRODUCT; ADDED N-LIST SWITCH "QMPW"
C        WHICH, WHEN TRUE, WILL QUALITY CONTROL TOTAL PRECIP. WATER
C        PRODUCT AGAINST RAIN CONTAMINATION FLAG AS IS DONE FOR
C        WIND SPEED PRODUCT
C 1998-01-28  D. A. KEYSER (NP22) -- WITH CHANGE IN W3LIB ROUTINE
C        W3MISCAN, LATEST NN ALGORITHM (IALG=1) IS NN3 WHICH RETURNS
C        BOTH WIND SPEED AND TOTAL PRECIP. WATER PRODUCTS (UNLIKE NN2
C        WHICH RETURNED ONLY WIND SPEED), ALSO NN3 CAN ONLY RETURN
C        PRODUCTS WITH NO RAIN CONTAMINATION SO Q.M. HERE IS HARDWIRED
C        TO '0'
C 1998-03-30  D. A. KEYSER (NP22) -- MODIFIED TO HANDLE NEURAL NET 3
C        SSM/I PRODUCTS INPUT IN A PRODUCTS BUFR DATA DUMP FILE
C 1998-05-19  D. A. KEYSER (NP22) -- INCREASED Y2K-COMPLIANCE SINCE
C        OUTPUT ON29 FILE NOW CONTAINS A Y2K COMPLIANT 40-CHARACTER
C        PSEUDO-ON85 HEADER LABEL (WAS THE 32-CHARACTER ON85 LABEL
C        WITH A 2-DIGIT YEAR)
C 1998-10-26  D.A. KEYSER -- PROGRAM NOW Y2K AND FORTRAN 90 COMPLIANT;
C        REMOVED PACKING OF RETRIEVAL LAT/LON INTO SINGLE ARRAY IN
C        SUPEROBING CASES, NOW STORE LAT AND LON IN SEPARATE ARAYS
C        (PACKED LAT/LON CARRYOVER FROM HDS VERSION WHERE MEMORY WAS
C        LIMITED)
C 1999-02-17  D.A. KEYSER -- OUTPUT FILE IS NOW IN BUFR (WAS IN
C        NCEP OFFICE NOTE 29 FORMAT)
C 2000-02-25  D.A. KEYSER -- CONVERTED TO RUN ON THE IBM-SP
C 2000-04-24  D.A. KEYSER -- SUPEROBS ON 0.5 DEGREE LAT/LON GRID CAN
C        NOW BE GENERATED OVER ANY LAT/LON DOMAIN (PRIOR TO THIS,
C        COULD ONLY BE GENERATED IN NORTHWESTERN HEMISPHERE)
C 2001-01-03  D.A. KEYSER -- MODIFIED TO RECOGNIZE THAT W3MISCAN NOW
C        RETURNS RAINFALL RATE AS 10**6 MM/SEC RATHER THAN AS WHOLE
C        MM/HR AND IT RETURNS SURFACE TEMPERATURE AS 10**2 K RATHER
C        THAN WHOLE K (MORE PRECISION NOW); STORES OUTPUT RAINFALL
C        RATE AS 10**6 MM/SEC (UNDER MNEMONIC "REQ6") RATHER THAN
C        10**4 MM/SEC (UNDER MNEMONIC "REQV") SINCE SUPEROBED VALUES
C        NEED A HIGHER PRECISION
C 2004-09-12  D.A. KEYSER -- MODIFIED TO RECOGNIZE THAT W3MISCAN NOW
C        RETURNS EITHER SURFACE TEMPERATURE OR SEA-SURFACE TEMPERATURE
C        IN THE OLD SURFACE TEMPERATURE SLOT (BASED ON SURFACE TAG),
C        CAN NOW PROCESS THE SEA-SURFACE TEMPERATURE PRODUCT FOUND
C        IN THE NCEP DATA DUMP PRODUCTS FILE; MODIFIED TO RECOGNIZE
C        MNEM. CHANGES FROM "PH2O" TO "TPWT", "REQ6" TO "REQV", "SNDP"
C        TO "TOSD" AND "CH2O" TO "METFET VILWC METFET" IN BUFR TABLE
C        USED TO OUTPUT REPROCESSED SSM/I FILE
C        (prepobs_prepssmi.bufrtable) {NOTE: FIRST METFET IS SET TO
C        12 (CLOUD), SECOND METFET IS SET TO MISSING (CANCEL)}
C 2011-08-04 D. KEYSER - IN RESPONSE TO THE LATEST VERSION OF BUFRLIB
C        WHICH CAN HANDLE EMBEDDED DICTIONARY MESSAGES: INCREASES
C        DEGREE OF BUFRLIB PRINTOUT SUCH THAT CODE WILL PRINT A
C        DIAGNOSTIC IF ANY EMBEDDED DICTIONARY MESSAGES ARE FOUND WHEN
C        READING IN MESSAGES; FOR NON-SUPEROB PROCESSING ONLY: REPLACES
C        CALL TO OPENMG (WHICH FORCED THE SAME CENTER BUFR DUMP DATE/
C        HOUR TO BE WRITTEN INTO SEC. 1 OF ALL OUTPUT MESSAGES) WITH
C        CALL TO OPENMB (ALLOWING SEC.1 IN OUTPUT MESSAGES TO HAVE SAME
C        DATE/HOUR AS THAT FROM SEC. 1 OF THE INPUT BUFR MESSAGE FROM
C        WHICH EACH SUBSET IS BEING PROCESSED), THIS FIXES A BUG WHICH
C        HAD CAUSED A BUFRLIB ABORT HERE WHEN THE INPUT BUFR FILE
C        CONTAINS EMBEDDED BUFR DICTIONARY MESSAGES, IT ALSO FORCES
C        OUTPUT FILE TO NOW COMPLY WITH NCEP BUFR STANDARD THAT ALL
C        SUBSETS IN A BUFR MESSAGE CONTAIN THE SAME YEAR, MONTH, DAY
C        AND HOUR AS THAT IN SEC. 1 OF THE BUFR MESSAGE; FOR SUPEROB
C        PROCESSING ONLY: MOVES CALL TO OPENMG FROM PRIOR TO READING OF
C        INPUT BUFR FILE TO AFTER READING OF INPUT BUFR FILE BUT PRIOR
C        TO WRITING OF OUTPUT BUFR FILE, THIS PREVENTS A BUFRLIB ABORT
C        WHEN THE INPUT BUFR FILE CONTAINS EMBEDDED BUFR DICTIONARY
C        MESSAGES (NOT SURE WHY!) (NOTE: WE USE OPENMG HERE RATHER THAN
C        OPENMB BECAUSE SUPEROBS ARE GENERATED AND ENCODED INTO THE
C        OUTPUT BUFR FILE ONLY AFTER THE ENTIRE INPUT BUFR FILE IS READ
C        - THE DATE IN SEC. 1 OF ALL BUFR MESSAGES IN THE OUTPUT
C        SUPEROB FILE IS SIMPLY THE DUMP CENTER DATE/HOUR, ALTHOUGH
C        THIS VIOLATES THE NCEP BUFR STANDARD THAT ALL SUBSETS IN A
C        BUFR MESSAGE CONTAIN THE SAME YEAR, MONTH, DAY AND HOUR AS
C        THAT IN SEC. 1 OF THE BUFR MESSAGE, IT KEEPS THE OUTPUT FILE
C        MORE COMPACT AND CAUSES NO HARM SINCE ONLY PREPOBS_PREPDATA
C        ENDS UP READING THIS FILE)
C 2012-11-20  J. WOOLLEN  INITIAL PORT TO WCOSS 
C 2020-08-20 J. DONG  - CHANGED MISSING VALUE TO 10E8 FROM 10E10.
C
C
C USAGE:
C   INPUT FILES:
C     UNIT 05  - DATA CARDS CONTAINING "NAMELIST"
C     UNIT 20  - BUFR MNEMONIC TABLE (NEEDED TO PRODUCE REPROCESSED
C                SSM/I BUFR OUTPUT FILE)
C     UNIT 25  - DIRECT ACCESS NESDIS LAND/SEA FILE
C     UNIT 26  - GRIB INDEX FILE FOR GRIB FILE CONTAINING GLOBAL
C              - 1-DEGREE SEA-SURFACE TEMPERATURE FIELD
C     UNIT 27  - GRIB FILE CONTAINING GLOBAL 1-DEGREE SEA-SURFACE
C              - TEMPERATURE FIELD
C     UNIT 31  - NCEP SSM/I BUFR DUMP FILE CONTAINING SCANS --
C                NOTE: EITHER THE PRODUCTS FILE {ssmip (FNOC) OR
C                ssmipn (NCEP)} OR THE B. TEMPERATURE FILE (ssmit)
C                DEPENDING ON THE VALUE FOR FOR SWITCH "IALG"
C                (SEE BELOW)
C         
C
C   OUTPUT FILES:
C     UNIT 06  - STANDARD OUTPUT PRINT
C     UNIT 51  - NCEP REPROCESSED SSM/I BUFR FILE CONTAINING
C                INDIVIDUAL RETRIEVALS (SEE ABSTRACT) WHICH MAY ALSO
C                BE SUPEROBED
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - STORE1   STORE2   STORE3   OBUFRS   OBUFRN
C                - SUPOB1   SUPOB2   FIRST(BLOCK DATA)
C     LIBRARY:
C       W3LIB    - W3FI04   W3MISCAN W3MOVDAT W3TAGB   W3TAGE
C                - ERREXIT
C       BUFRLIB  - DATELEN  OPENBF   READMG   CLOSBF   OPENMG
C                - UFBINT   WRITSB   DUMPBF   MINIMG   UFBCNT
C                - CLOSMG   UFBREP   OPENMB
C
C   EXIT STATES:
C     COND =   0 - SUCCESSFUL RUN
C          =  16 - ERROR READING SSM/I DATA FILE (VIA SUBROUTINE
C                  W3MISCAN)
C          =  18 - SUPEROB SWITCH TURNED ON AND ADDITIONAL PRODUCTS IN
C                  IVAR(8) REQUESTED; ADDITIONAL PRODUCTS CANNOT BE
C                  SUPEROBED
C          =  19 - BRIGHTNESS TEMP AND ONE OR MORE PRODUCTS REQUESTED
C                  (REGARDLESS OF SUPEROB SWITCH) - CANNOT COMBINE
C                  PROCESSING OF B. TEMPS AND PRODUCTS
C          =  61 - ERROR OBTAINING CENTER DATE FROM FIRST BUFR MESSAGE
C                  IN UNIT 31
C          =  62 - ERROR OBTAINING DUMP DATE FROM SECOND BUFR MESSAGE
C                  IN UNIT 31
C
C REMARKS: PROGRAM TESTS ALL UNPACKED PRODUCTS/BRIGHTNESS TEMPS FOR
C   NEGATIVE VALUES; IF FOUND, PRODUCTS ARE SKIPPED AND BRIGHTNESS
C   TEMPS ARE SET TO MISSING) (FOR WIND SPEED, PRODUCT SKIPPED
C   REGARDLESS OF ITS QUALITY MARKER).
C
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  WCOSS
C
C$$$
CC
CC
C   ***** VARIABLES IN NAMELIST SWTCH READ IN MAIN PROGRAM *****
CC
C   LSAT    - 10-WORD LOGICAL ARRAY (240:249) INDICATING WHICH
C              SATELLITE IDS SHOULD BE PROCESSED
C
C         LSAT(X) = TRUE -- PROCESS SCANS FROM SATELLITE ID X (WHERE X
C                           IS CODE FIG. FROM BUFR CODE TABLE 0-01-007)
C         LSAT(X) = FALSE - DO NOT PROCESS SCANS FROM SATELLITE ID X
C
C                   X = 240 IS F-7  DMSP SATELLITE (HARDWIRED AS FALSE
C                           SINCE THIS SATELLITE IS NO LONGER AVAILABLE)
C                   X = 241 IS F-8  DMSP SATELLITE (HARDWIRED AS FALSE
C                           SINCE THIS SATELLITE IS NO LONGER AVAILABLE)
C                   X = 242 IS F-9  DMSP SATELLITE (HARDWIRED AS FALSE
C                           SINCE THIS SATELLITE IS NO LONGER AVAILABLE)
C                   X = 243 IS F-10 DMSP SATELLITE (THIS IS AVAILABLE
C                           AND IS CONSIDERED TO BE THE OPERATIONAL
C                           EVEN DMSP SSM/I SATELLITE AS OF 8/1996)
C                   X = 244 IS F-11 DMSP SATELLITE (THIS IS AVAILABLE
C                           AS OF 8/96 BUT IS NOT CONSIDERED TO BE AN
C                           OPERATIONAL DMSP SSM/I SATELLITE)
C                   X = 245 IS F-12 DMSP SATELLITE (HARDWIRED AS FALSE
C                           SINCE THIS SATELLITE IS NO LONGER AVAILABLE)
C                   X = 246 IS F-13 DMSP SATELLITE (THIS IS AVAILABLE
C                           AND IS CONSIDERED TO BE THE OPERATIONAL
C                           ODD  DMSP SSM/I SATELLITE AS OF 8/1996)
C                   X = 247-249 ARE RESERVED FOR FUTURE DMSP SATELLITES
C
C        NOTE: HERE "EVEN" MEANS VALUE IN IBUFTN(1) IS AN ODD NUMBER
C              WHILE "ODD" MEANS VALUE IN IBUFTN(1) IS AN EVEN NUMBER;
C              IF MORE THAN 2 SATELLITES ARE SELECTED IN "LSAT", THEN
C              THE VARIOUS "EVEN" OR "ODD" SATELLITES WILL BE LUMPED
C              TOGETHER IN TAGGING THE PREFIX CHARACTER OF THE OUTPUT
C              STN. ID OF NON-SUPEROBED RETRIEVALS IN SUBR. OBUFRN!!}
C
CC
C   ISUPOB  - SUPEROB SWITCH
C             NOTE: THERE IS NO DEFAULT, THIS MUST BE SPECIFIED IN THE
C                   DATA CARDS BECAUSE THE SCRIPT GREPS ON THE VALUE
C                   FOR ISUPOB IN ORDER TO DETERMINE THE MAKEUP OF THE
C                   BUFR MNEMONIC TABLE USED TO MAKE THE OUTPUT FILE
C        ISUPOB = 0 -- SUPEROBS ARE NOT GENERATED; ORIGINAL (BUT
C                      REPROCESSED) RETRIEVALS ARE PACKED INTO OUTPUT
C                      BUFR FILE
C        ISUPOB = 1 -- SUPEROBS ARE GENERATED BY TAKING LINEAR AVERAGE
C                      OF ALL VALID DATA/TIMES WITHIN SPECIFIED GRID
C                      (LAT/LON) BOX; SUPEROB IS PLACED AT POINT W/I
C                      BOX REPRESENTED BY LINEAR AVERAGED LATITUDE AND
C                      LONGITUDE (EACH PRODUCT TREATED SEPARATELY)
C    (NOTE: ISUPOB=1 NOT VALID FOR IVAR(8) = 0)
CC
C   DELAT   - LATITUDE  SPACING (DEGREES) OF SUPEROB GRID BOX
C    {NOTE: NORMALLY WHOLE DEGREES EXCEPT FOR CHOICE OF 0.5 DEGREES
C   DELON   - LONGITUDE SPACING (DEGREES) OF SUPEROB GRID BOX
C    {NOTE: NORMALLY WHOLE DEGREES EXCEPT FOR CHOICE OF 0.5 DEGREES
C
C    (NOTE: DELAT AND DELON ARE MEANINGLESS WHEN ISUPOB = 0)
CC
C   IVAR    - PROCESS/NO PROCESS THE FOLLOWING (DIMENSION 10)
C           WORD 1  -- SURFACE WIND SPEED PRODUCT OVER OCEAN
C                      (SEE SWITCH "IALG" FOR MORE INFO, ON THIS)
C           WORD 2  -- TOTAL COLUMN PRECIP. WATER PRODUCT OVER OCEAN
C                      (SEE SWITCH "IALG" FOR MORE INFO, ON THIS)
C           WORD 3  -- RAINFALL RATE PRODUCT
C           WORD 4  -- SURFACE TEMPERATURE PRODUCT
C                       (ONLY AVAILABLE IF SURFACE TAG IS NOT OCEAN)
C           WORD 5  -- CLOUD WATER PRODUCT OVER OCEAN
C           WORD 6  -- SOIL MOISTURE PRODUCT
C           WORD 7  -- SNOW DEPTH PRODUCT
C           WORD 8  -- SEA-SURFACE TEMPERATURE PRODUCT
C                       (ONLY AVAILABLE IF SURFACE TAG IS OCEAN)
C           WORD 9  -- ADDITIONAL PRODUCTS (SURFACE TAG,
C                      ICE CONCENTRATION, ICE AGE, ICE EDGE,
C                      CALCULATED SURFACE TYPE) - ALL OR NONE HERE
C           WORD 10 -- SEVEN-CHANNELS OF BRIGHTNESS TEMPERATURES:
C                      19 GHZ V, 19 GHZ H, 22 GHZ V, 37 GHZ V,
C                      37 GHZ H, 85 GHZ V, 85 GHZ H) - ALL OR NONE HERE
C                      (SEE SWITCH "IALG" FOR MORE INFO, ON THIS)
C        IVAR(1) - (10) = 0    -- PROCESS THIS DATUM
C        IVAR(1) - (10) = 9999 -- DON'T PROCESS THIS DATUM
C    (NOTE1: REGARDLESS OF ISUPOB VALUE: IF IVAR(10) = 0, THEN
C           IVAR(1)-(8) MUST BE 9999; IF ONE OR MORE OF
C           IVAR(1)-(8) = 0, THEN IVAR(10) MUST BE 9999)
C    (NOTE2: FOR IVAR(1)-IVAR(8) PRODUCTS, A UNIQUE REPORT IS GENERATED
C           FOR EACH PRODUCT, EACH PRODUCT IS PACKED AS A UNIQUE
C           SUBSET IN THE OUTPUT BUFR FILE; FOR IVAR(10), A UNIQUE
C           REPORT IS GENERATED CONTAINING THE SEVEN-CHANNELS OF
C           BRIGHTNESS TEMPS, EACH REPORT IS PACKED AS A UNIQUE SUBSET
C           IN THE OUTPUT BUFR FILE; FOR IVAR(9), A UNIQUE REPORT IS
C           GENERATED CONTAINING ALL 5 PRODUCTS (UNLESS A PRODUCT IS
C           MISSING, THEN IT IS NOT INCLUDED), EACH REPORT IS PACKED
C           AS A UNIQUE SUBSET IN THE OUTPUT BUFR FILE  A N D   ISUPOB
C           MUST BE 0 IF IVAR(9) IS SELECTED.
C    (NOTE3: IF INPUT FILE IS NCEP DERIVED PRODUCTS, CHANCES ARE ONLY
C           WIND SPEED PRODUCT AND TOTAL COLUMN PRECIP. WATER PRODUCT
C           WOULD BE AVAILABLE, ALONG WITH SURFACE TAG.
CC
C   IALG    - ALGORITHM FOR CONVERTING BRIGHTNESS TEMPERATURES TO
C             WIND SPEED AND/OR TOTAL PRECIP. WATER PRODUCT VIA IN-LINE
C             ALGORITHM
C             NOTE: THERE IS NO DEFAULT, THIS MUST BE SPECIFIED IN THE
C                   DATA CARDS BECAUSE THE SCRIPT GREPS ON THE VALUE
C                   FOR ALG IN ORDER TO DETERMINE IF THE ssmip/ssmipn
C                   OR ssmit FILE SHOULD BE INPUT TO THE PROGRAM IN
C                   UNIT 31
C        IALG   = 0 -- PRODUCTS FILE (EITHER FNOC "OPERATIONAL" OR
C                      NCEP DERIVED) IS INPUT.  NO ALGORITHM USED. IF
C                      IVAR(1)=0, WIND SPEED IS OBTAINED DIRECTLY FROM
C                      DERIVED VALUE IN INPUT FILE.  IF IVAR(2)=0,
C                      PRECIP. WATER IS OBTAINED DIRECTLY FROM DERIVED
C                      VALUE IN INPUT FILE.   IVAR(1)-(9) CAN BE EITHER
C                      0 OR 9999.
C        IALG   = 1 -- BTEMP FILE IS INPUT.  WIND SPEED AND PRECIP.
C                      WATER ARE OBTAINED VIA IN-LINE CALCULATION USING
C                      LATEST NEURAL NETWORK ALGORITHM ON B. TEMPS
C        IALG   = 2 -- BTEMP FILE IS INPUT.  WIND SPEED (ONLY) IS 
C                      OBTAINED VIA IN-LINE CALCULATION USING
C                      GOODBERLET ALG. ON B. TEMPS
C        IALG   =99 -- BTEMP FILE IS INPUT.  NO CONVERSION TO PRODUCTS
C                      IS PERFORMED - BRIGHTNESS TEMPS ARE OUTPUT
C                      (IVAR(10)=0).
C    {NOTE: IALG=1 APPLIES ONLY WHEN IVAR(1) AND/OR IVAR(2) = 0 AND
C           IVAR(3)-(8) = 9999; IALG=2 APPLIES ONLY WHEN IVAR(1) = 0 AND
C           IVAR(2)-(8) = 9999}
CC
C   IQM     - FOR GOODBERLET ALG. WIND SPEED PRODUCT {IVAR(1)}, EITHER
C             FROM INPUT FNOC (ONLY) PRODUCTS FILE, OR CALC. IN-LINE
C             FROM B. TEMP INPUT FILE {AND POSSIBLY FOR TOTAL PRECIP.
C             WATER PRODUCT, IVAR(2), FROM INPUT FNOC (ONLY) PRODUCTS
C             FILE, SEE SWITCH "QMPW"}:
C             THE LOWEST (POOREST) QUALITY MARK ACCEPTED FOR PROCESSING
C               THE QUALITY MARKERS ARE DEFINED AS FOLLOWS:
C                   = 0 -- NO RAIN CONTAMINATION (ERROR < 2 M/S)
C                   = 1 -- SLIGHT RAIN CONTAMINATION (ERROR 2-5 M/S)
C                   = 2 -- MODERATE RAIN CONTAMINATION (ERROR 5-10 M/S)
C                   = 3 -- SEVERE RAIN CONTAMINATION (ERROR > 10 M/S)
C               = 99999 -- MISSING (W. SPEED SHOULD ALSO BE MISSING)
C        IQM = 0 -- ONLY WIND SPEEDS WITH Q.M.= 0 PROCESSED
C        IQM = 1 -- ONLY WIND SPEEDS WITH Q.M.= 0 OR 1 PROCESSED
C        IQM = 2 -- ONLY WIND SPEEDS WITH Q.M.= 0, 1 OR 2 PROCESSED
C        IQM = 3 -- ALL WIND SPEEDS PROCESSED REGARDLESS OF Q.M. VALUE
C                    EXCEPTION: NEGATIVE VALUES ALWAYS REJECTED)
C    (NOTE: I M P O R T A N T:  THIS SWITCH DOES NOT APPLY FOR LATEST
C           NEURAL NET ALGORITHM WIND SPEED AND/OR PRECIP. WATER EITHER
C           READ IN FROM PRODUCTS FILE OR CALC. IN-LINE FROM INPUT B.
C           TEMPS; HERE ONLY RETRIEVALS WITH NO RAIN CONTAMINATION ARE
C           PROCESSED BY THE ALGORITHM)
C
C   QMPW    - LOGICAL INDICATING WHETHER THE INPUT FNOC (ONLY) TOTAL
C             PRECIPITABLE WATER PRODUCT SHOULD BE QUALITY CONTROLLED
C             AGAINST THE GOODBERLET ALG. WIND SPEED "QUALITY MARKER"
C             (RAIN CONTAMINATION FLAG)
C        QMPW   = TRUE  -- SWITCH "IQM" IS THEN ALSO USED FOR TPW
C        QMPW   = FALSE -- NO TPW QUALITY CONTROL IS PERFORMED (DEFAULT)
C    {NOTE1: IF THE QUALITY MARKER IS MISSING (99999) THE TPW PRODUCT
C            IS NEVER PROCESSED WHEN QMPW = TRUE
C     NOTE2: APPLIES ONLY WHEN IVAR(2) = 0 AND GOODBERLET ALG. IS USED
C            (EITHER FNOC PROD. WHEN IALG=0, OR CASE WHERE IALG=2 - IT
C            NEVER APPLIES TO LATEST NEURAL NET ALG. TPW CALC. IN-LINE
C            FROM B. TEMPS (IALG=1) OR NCEP PROD. INPUT WHEN IALG=0}
CC
C      THE FOLLOWING 4 SWITCHES INDICATE THE CENTRAL TIME FOR THE DATA
C    (NOTE: IF IYEAR IS SET TO '0000' -- THIS TIME IS READ IN FROM
C           THE CENTER DATE (FIRST) BUFR MESSAGE IN UNIT 31)
C
C   IYEAR   - 4-DIGIT YEAR
C   IMONTH  - 2-DIGIT MONTH OF THE YEAR
C   IDAY    - 2-DIGIT DAY OF THE MONTH
C   IHOUR   - 2-DIGIT HOUR OF THE DAY
C   IMIN    - 2-DIGIT MINUTE OF THE HOUR
C
C   IWINDO  - THIS INDICATES THE TIME WINDOW ABOUT THE CENTRAL TIME
C             FOR PROCESSING THE DATA
C      IWINDO =  30  -- ACCEPT DATA FROM 30 MIN. BEFORE TO 29 MIN. AFTER
C                       THE CENTRAL TIME
C      IWINDO = 100  -- ACCEPT DATA FROM 1-HR BEFORE TO 59 MIN. AFTER
C                       THE CENTRAL TIME
C      IWINDO = 130  -- ACCEPT DATA FROM 1-HR 30-MIN BEFORE TO 1-HR
C                       29-MIN AFTER THE CENTRAL TIME
C      IWINDO = 300  -- ACCEPT DATA FROM 3-HR BEFORE TO 2-HR 59 MIN.
C                       AFTER THE CENTRAL TIME
C             -- ETC. --
C    (NOTE: IWINDO SHOULD  N E V E R   EXCEED 600)
CC
C      THE FOLLOWING 4 SWITCHES INDICATE THE LATITUDE/LONGITUDE
C                    BOUNDARY FOR ACCEPTING DATA
C
C   LATS    - THIS IS THE SOUTHERN BDRY {LAT: DEG. N (+); DEG. S (-)}
C   LATN    - THIS IS THE NORTHERN BDRY {LAT: DEG. N (+); DEG. S (-)}
C   LONW    - THIS IS THE WESTERN  BDRY {LON: 0-360 DEG. W}
C   LONE    - THIS IS THE EASTERN  BDRY {LON: 0-360 DEG. W}
C    (NOTE: THESE ARE ALWAYS INTEGERS; MUST BE WHOLE DEGREES)
CC
C   LIMCNT  - LIMITING NUMBER OF INDIVIDUAL RETRIEVALS FOR WHICH A
C             SUPEROB IS GENERATED {I.E., IF LESS THAN 'LIMCNT'
C             RETRIEVALS ARE FOUND IN THE GRID (LAT/LON) BOX, THEN
C             A SUPEROB IS NOT GENERATED FOR THIS BOX}
C    (NOTE: THIS SWITCH APPLIES ONLY WHEN ISUPOB = 1; THE DEFAULT
C           VALUE IS LIMCNT = 1; LIMCNT SHOULD NEVER BE LESS THAN 1)
CC

      PROGRAM PREPOBS_PREPSSMI

      LOGICAL  ITMFL,LPROD,LBRIT,NNALG,GBALG,LSAT(240:249),QMPW,NCEPRD

      CHARACTER*8  SUBSET,SUBOUT
      CHARACTER*10  TEXT0(2)
      CHARACTER*34  TEXT(10)
      CHARACTER*47  TEXTPR(0:2)

      INTEGER  KDATE(8),LDATE(8),ICDATE(5),IDDATE(5),NRF(0:4,2)

      COMMON/SCAN/IBUFTN(1737)
      COMMON/KOUNT/ICNT(10),ICNTT,NFILE,IRECL,ISUBL,ISUBT
      COMMON/BGRID/BLAT(360),BLON(720),NOBS(8,720,360)
      COMMON/DGRID/LATSIZ,LONSIZ
      COMMON/SWITCH/IVAR(10),IQM,ISUPOB,LATS,LATN,LONW,LONE,LIMCNT,QMPW
      COMMON/SAVED/ITOBS(9),ITOBST,DGH,DGV,SUMDTA(8,720,360),
     $ SSQDTA(8,720,360),SUMTIM(8,720,360),SUMLAT(8,720,360),
     $ SUMLON(8,720,360)
      COMMON/CENTER/IDATE(8)

      NAMELIST/SWTCH/DELAT,DELON,LSAT,ISUPOB,IVAR,IQM,IYEAR,IMONTH,
     $ IDAY,IHOUR,IMIN,IWINDO,LATS,LATN,LONW,LONE,LIMCNT,IALG,QMPW

      DATA  TEXT0/'RETRIEVALS',' SUPEROBS '/
      DATA  TEXT /'    OCEANIC WIND SPEED PRODUCT    ',
     $            'OCEANIC PRECIPITABLE WATER PRODUCT',
     $            '      RAINFALL RATE PRODUCT       ',
     $            '   SURFACE TEMPERATURE PRODUCT    ',
     $            '   OCEANIC CLOUD WATER PRODUCT    ',
     $            '      SOIL MOISTURE PRODUCT       ',
     $            '        SNOW DEPTH PRODUCT        ',
     $            ' SEA-SURFACE TEMPERATURE PRODUCT  ',
     $            'ADDITIONAL PRODUCTS (SEE * BELOW) ',
     $            'BRIGHTNESS TEMPERAURE - 7 CHANNELS'/
      DATA  TEXTPR/'     OBTAINED FROM INPUT PRODUCTS DATA SET     ',
     $             'CALCULATED IN-LINE VIA NEURAL NETWORK ALGORITHM',
     $             '  CALCULATED IN-LINE VIA GOODBERLET ALGORITHM  '/

      CALL W3TAGB('PREPOBS_PREPSSMI',2020,0233,0057,'NP22')

C  CALL W3FI04 TO DETERMINE MACHINE WORD LENGTH (BYTES)
C   AND TO TEST FOR ASCII(ICHTP=0) OR EBCDIC(ICHTP=1) CHARACTERS
C  -------------------------------------------------------------

      CALL W3FI04(IENDN,ICHTP,LW)
      PRINT 2213, LW,ICHTP,IENDN
 2213 FORMAT(/' ---> CALL TO W3FI04 RETURNS: LW = ',I3,', ICHTP = ',I3,
     $ ', IENDN = ',I3/)

      LIMCNT = 1
      NRF = 0
      QMPW = .FALSE.
      READ(5,SWTCH)
      LSAT(240:242) = .FALSE.
      LSAT(245) = .FALSE.
      IUNIT = 31

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

      CALL DATELEN(10)

C  GET THE CENTER & DUMP DATE FROM INPUT ORIGINAL BUFR DATA DUMP FILE
C  ------------------------------------------------------------------

      CALL DUMPBF(IUNIT,ICDATE,IDDATE)
      WRITE(6,*) ' '
      WRITE(6,*) 'From Original SSM/I Bufr Data Dump File in Unit 31:'
      WRITE(6,*) '     - Center date (ICDATE) = ',ICDATE
      WRITE(6,*) '     - Dump date   (IDDATE) = ',IDDATE
      WRITE(6,*) 'Will transfer these to output reprocessed SSM/I BUFR',
     $ ' file in Unit 51 (SPSSMI)'
      WRITE(6,*) ' '
      IF(ICDATE(1).LE.0)  THEN

C  IF CENTER DATE COULD NOT BE READ FROM FIRST DUMMY MESSAGE,
C  STOP WITH CONDITION CODE 61
C  ----------------------------------------------------------

         WRITE(6,*) 'DUMPBF ERROR - CENTER DATE COULD NOT BE READ ',
     $    'FROM INPUT SSM/I DATA DUMP FILE -- STOP 61'
         CALL W3TAGE('PREPOBS_PREPSSMI')
         CALL ERREXIT(61)
      END IF
      IF(IDDATE(1).LE.0)  THEN

C  IF DUMP DATE COULD NOT BE READ FROM SECOND DUMMY MESSAGE,
C  STOP WITH CONDITION CODE 62
C  ---------------------------------------------------------

         WRITE(6,*) 'DUMPBF ERROR - DUMP DATE COULD NOT BE READ ',
     $    'FROM INPUT SSM/I DATA DUMP FILE -- STOP 62'
         CALL W3TAGE('PREPOBS_PREPSSMI')
         CALL ERREXIT(62)
      END IF

C  OPEN INPUT BUFR FILE TO SEE IF IT CONTAINS NCEP SSM/I PRODUCTS
C   (NEED TO KNOW THIS PRIOR TO READING IN SCANS, IALG=0 DOESN'T
C   PROVIDE THIS INFORMATION, ONLY THAT INPUT FILE IS A PRODUCTS
C   DUMP - EITHER FNOC OR NCEP)
C  --------------------------------------------------------------

      CALL OPENBF(IUNIT,'IN',IUNIT)
         call openbf(0,'QUIET',1)

C  READ FIRST BUFR MESSAGE IN THE INPUT BUFR FILE TO GET SUBSET NAME
C  -----------------------------------------------------------------

      CALL READMG(IUNIT,SUBSET,JDATBF,IRET)
      IF(IRET.NE.0)   THEN
C.......................................................................

C  PROBLEM: INPUT FILE IS EMPTY!!! -- STOP WITH C. CODE 16
C  -------------------------------------------------------

         PRINT 14
   14    FORMAT(/' ##INPUT BUFR/SSM-I FILE CONTAINS NO MESSAGES'/)
         IER = 2
C  ERROR READING A SCAN LINE
         PRINT 109, IUNIT,IER
         CALL W3TAGE('PREPOBS_PREPSSMI')
         CALL ERREXIT(16)
C.......................................................................
      END IF
      SUBOUT = SUBSET
      NCEPRD = (SUBSET.EQ.'NC012103')
      CALL CLOSBF(IUNIT)
      REWIND IUNIT
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C  N.NET PRODUCTS (FROM NCEP PROD FILE OR CALC. IN-LINE) ONLY HAVE IQM=0
      IF(IALG.EQ.1.OR.NCEPRD)  IQM = 0
C IF NEURAL NET ALGORITHM TPW PRODUCT (EITHER FROM INPUT NCEP PRODUCTS
C  FILE OR FROM CALC. IN-LINE), QMPW ALWAYS FALSE
      IF(IVAR(2).EQ.0.AND.(IALG.EQ.1.OR.NCEPRD))  QMPW = .FALSE.
      WRITE(6,SWTCH)
      IF(IVAR(10).EQ.0.AND.MIN(IVAR(1),IVAR(2),IVAR(3),IVAR(4),IVAR(5),
     $ IVAR(6),IVAR(7),IVAR(8)).EQ.0)  THEN
C.......................................................................

C  CAN'T PROCESS BOTH BRIGHTNESS TEMP AND ONE OR MORE PRODUCTS - STOP 19
C   (REGARDLESS OF ISUPOB)
C  ---------------------------------------------------------------------
         PRINT 119
         CALL W3TAGE('PREPOBS_PREPSSMI') 
         CALL ERREXIT(19)
C.......................................................................
      END IF
      IF(ISUPOB.EQ.1)  THEN
         IF(IVAR(9).EQ.0)  THEN
C.......................................................................

C  IF SUPEROBING, CAN'T PROCESS ADD'L PRODUCTS IN IVAR(9) - STOP 18
C  ----------------------------------------------------------------

            PRINT 118
            CALL W3TAGE('PREPOBS_PREPSSMI') 
            CALL ERREXIT(18)
C.......................................................................
         END IF
         LATSIZ = 180./DELAT + 0.5
         LONSIZ = 360./DELON + 0.5
         DGH = DELON/2.
         DGV = DELAT/2.
C***********************************************************************

C  BLAT & BLON REPRESENT THE CENTER LAT/LON FOR THE SELECTED GRID
C   BOXES OVERWHICH SUPEROBING IS DONE:
C  --------------------------------------------------------------
C    RANGE:  -90 TO + 90 LAT; 0 TO 360 WEST LON

         LATBEG = 1
         K = 0
         DO LAT  = LATBEG,LATSIZ
            K = K + 1
            BLAT(K) = (DELAT * ((LAT - 1) - LATSIZ/2)) + DGV
         ENDDO
         DO LONG = 1,LONSIZ
            BLON(LONG) = (DELON * (LONG - 1)) + DGH
         ENDDO
C.......................................................................
      END IF
C***********************************************************************

C  IF INPUT YEAR FROM NAMELIST IS '00': USE CENTER DATE FROM INPUT
C   SSM/I BUFR DATA DUMP FILE AS BOTH CENTER DATE IN OUTPUT
C   REPROCESSED BUFR FILE AND AS CENTER DATE FOR CALCULATING TIME
C   WINDOW DATES THAT ARE PASSED TO W3MISCAN
C  IF INPUT YEAR FROM NAMELIST IS NOT '00': USE DATE FROM INPUT DATA
C   CARDS AS BOTH CENTER DATE IN OUTPUT REPROCESSED BUFR FILE AND AS
C   CENTER DATE FOR CALCULATING TIME WINDOW DATES THAT ARE PASSED TO
C   W3MISCAN
C  -----------------------------------------------------------------

      IF(IYEAR.EQ.0000)  THEN
         IYEAR  = ICDATE(1)
         IMONTH = ICDATE(2)
         IDAY   = ICDATE(3)
         IHOUR  = ICDATE(4)
         IMIN   = ICDATE(5)
      END IF

C  CONSTRUCT CENTER DATE FOR OUTPUT
C  --------------------------------

      PRINT 100, IYEAR,IMONTH,IDAY,IHOUR,IMIN,IWINDO
      DO II = 1,10
         IF(IVAR(II).EQ.0)  PRINT 101, TEXT(II)
      ENDDO
      IALGPW = IALG
      IF(MIN(IVAR(1),IVAR(2),IVAR(5),IVAR(8)).EQ.0)  THEN
         IF(IVAR(1).EQ.0)  THEN
            PRINT 901, TEXTPR(IALG)
  901       FORMAT(/52X,'OCEANIC WIND SPEED PRODUCT'/41X,A47)
            IF(IALG.EQ.0)  THEN
               IF(.NOT.NCEPRD)  THEN
                  PRINT 915
  915             FORMAT(53X,'VIA GOODBERLET ALGORITHM')
               ELSE
                  PRINT 8915
 8915             FORMAT(49X,'VIA LATEST NEURAL NET ALGORITHM')
               END IF
            END IF
            IF(IALG.NE.1.AND..NOT.NCEPRD)  THEN
               PRINT 916, IQM
  916          FORMAT(40X,'ONLY RAIN FLAG CATEGORY',I3,' AND LESS ARE ',
     $          'PROCESSED'/)
            ELSE
               PRINT 2916
 2916          FORMAT(43X,'ONLY RAIN FLAG CATEGORY ZERO IS AVAILABLE')
            END IF
         END IF
         IF(IVAR(2).EQ.0)  THEN
            IF(IALG.EQ.2)  IALGPW = 0
            PRINT 1901, TEXTPR(IALGPW)
 1901 FORMAT(/45X,'OCEANIC TOTAL PRECIPITABLE WATER PRODUCT'/41X,A47)
            IF(IALGPW.EQ.0)  THEN
               IF(.NOT.NCEPRD)  THEN
                  PRINT 915
               ELSE
                  PRINT 8915
               END IF
            END IF
            IF(IALG.EQ.1.OR.NCEPRD)  PRINT 2916
         END IF
         IF(IALG.EQ.0)  THEN
            IF(IVAR(5).EQ.0)  THEN
               PRINT 1908, TEXTPR(IALG)
 1908 FORMAT(/52X,'OCEANIC CLOUD WATER PRODUCT'/41X,A47)
               IF(.NOT.NCEPRD)  THEN
                  PRINT 915
               ELSE
                  PRINT 8915
               END IF
            END IF
            IF(IVAR(8).EQ.0)  THEN
               PRINT 1909, TEXTPR(IALG)
 1909 FORMAT(/45X,'OCEANIC SEA-SURFACE TEMPERATURE PRODUCT'/41X,A47)
               IF(.NOT.NCEPRD)  THEN
                  PRINT 915
               ELSE
                  PRINT 8915
               END IF
            END IF
         END IF
      ELSE
         IALG = 0
      END IF
      IF(IVAR(2).EQ.0.AND.QMPW)  THEN
         PRINT 889, IQM
  889 FORMAT(/9X,'FOR TOTAL PRECIP. WATER, ONLY RAIN FLAG CATEGORY',I3,
     $ ' AND LESS ARE PROCESSED (TPW NEVER PROCESSED IF R.F. MISSING)')
         IF(IVAR(1).EQ.0)  THEN
            PRINT 890
  890 FORMAT(39X,'RAIN FLAG BASED ON WIND SPEED ALGORITHM NOTED ABOVE'/)
         ELSE
            PRINT 891
  891 FORMAT(30X,'RAIN FLAG BASED ON GOODBERLET WIND SPEED ALGORITHM ',
     $ 'AS INPUT FROM PRODUCTS FILE'/)
         END IF
      END IF
      IF(IVAR(9).EQ.0)  PRINT 1101

C#######################################################################

      IUNITT = 20

C  OPEN OUTPUT BUFR FILE WHICH WILL CONTAIN REPROCESSED SSM/I DATA
C  ---------------------------------------------------------------

      CALL OPENBF(NFILE,'OUT',IUNITT)
      WRITE(6,1905) NFILE,IUNITT
 1905 FORMAT(/8X,'===> OUTPUT BUFR DATA SET IN UNIT',I3,' SUCCESSFULLY',
     $ ' OPENED FOR OUTPUT; BUFR MNEMONIC TABLES A,B,D IN UNIT',I3/13X,
     $'READ IN AND ENCODED INTO BEGINNING MESSAGES OF OUTPUT DATA SET'/)

C  TRANSFER CENTER & DUMP DATE FROM INPUT FILE TO OUTPUT BUFR FILE
C  ---------------------------------------------------------------

      IDATBF=IYEAR*1000000+IMONTH*10000+IDAY*100+IHOUR
      IDATDM=IDDATE(1)*1000000+IDDATE(2)*10000+IDDATE(3)*100+IDDATE(4)

C  ESTABLISH PROPER SUBSET NAME FOR BUFR MESSAGES IN OUTPUT BUFR FILE
C   (BASED ON SUBSET FROM INPUT BUFR FILE AS WELL AS IALG VALUE)
C  ------------------------------------------------------------------

      IF(SUBSET.EQ.'NC012001')  THEN
         IF(IALG.EQ.1)  THEN
            SUBSET = 'NC012103'
         ELSE  IF(IALG.EQ.2)  THEN
            SUBSET = 'NC012002'
         END IF
      END IF
        
      CALL OPENMG(NFILE,SUBOUT,IDATBF)
      CALL MINIMG(NFILE,IMIN)
      CALL CLOSMG(NFILE)
      CALL OPENMG(NFILE,SUBOUT,IDATDM)
      CALL MINIMG(NFILE,IDDATE(5))
      CALL CLOSMG(NFILE)

C  SET-UP THE TIME WINDOW HERE (BASED ON INPUT 'IWINDO')
C  -----------------------------------------------------

      IDATE    = 0
      IDATE(1) = IYEAR
      IDATE(2) = IMONTH
      IDATE(3) = IDAY
      IDATE(5) = IHOUR
      IDATE(6) = IMIN
      IUPDAT = ((IWINDO/100) * 60) + MOD(IWINDO,100)

C  BACKDATE CENTRAL DATE/TIME BY IWINDO: KDATE
C  -------------------------------------------

      CALL W3MOVDAT((/0.,0.,REAL(-IUPDAT), 0.,0./),IDATE,KDATE)

C  UPDATE CENTRAL DATE/TIME BY IWINDO (MINUS 1 MIN): LDATE
C  -------------------------------------------------------

      CALL W3MOVDAT((/0.,0.,REAL(IUPDAT-1),0.,0./),IDATE,LDATE)

C#######################################################################

      PRINT 106, DELAT,DELON,LSAT,ISUPOB,IVAR,IQM,QMPW,LATS,LATN,LONW,
     $ LONE,LIMCNT,IALG
      PRINT 107, (KDATE(III),III=1,3),KDATE(5),KDATE(6),
     $ (LDATE(III),III=1,3),LDATE(5),LDATE(6)

C  ITMFL=TRUE LATER ADDS 2400. TO TIMES INTO NEXT DAY FOR SUPEROBING
C  -----------------------------------------------------------------

      ITMFL = (KDATE(5).GT.LDATE(5))

C  WILL WE NEED TO DECODE INPUT PRODUCTS (EITHER FNOC OR NCEP)?
C  ------------------------------------------------------------

      LPROD = (MIN(IVAR(3),IVAR(4),IVAR(5),IVAR(6),IVAR(7),IVAR(8),
     $ IVAR(9)).EQ.0.OR.(IVAR(1).EQ.0.AND.(IALG.EQ.0.OR.NCEPRD)).OR.
     $ (IVAR(2).EQ.0.AND.(IALGPW.EQ.0.OR.NCEPRD)))

C  WILL WE NEED TO DECODE BRIGHTNESS TEMPERATURES?
C  -----------------------------------------------

      LBRIT = (IVAR(10).EQ.0.OR.(IVAR(1).EQ.0.AND.IALG.GT.0).OR.(IVAR(2)
     $ .EQ.0.AND.IALGPW.EQ.1))

C  ARE WE SELECTING W.SPEED & TPW AS CALC IN-LINE VIA LATEST N.NET ALG?
C  --------------------------------------------------------------------

      NNALG = (LBRIT.AND.IALG.EQ.1)

C  ARE WE SELECTING W.SPEED AS CALC. IN-LINE VIA GOODBERLET ALGORITHM?
C  -------------------------------------------------------------------
      GBALG = (LBRIT.AND.IALG.EQ.2)

C  INITALIZE ALL SUMS AS ZERO
C  --------------------------

      NOBS   = 0
      SUMDTA = 0.
      SSQDTA = 0.
      SUMTIM = 0.
      SUMLAT = 0.
      SUMLON = 0.
      INLSF  = 25
      INGBI  = 26
      INGBD  = 27

C  IGNRTM IS HARDWIRED AS ZERO - TELLS W3MISCAN TO TIME CHECK SCANS
C  ----------------------------------------------------------------

      IGNRTM = 0

C-----------------------------------------------------------------------
C    READ A SCAN LINE HOLDING 64 RETRIEVALS INTO ARRAY IBUFTN
C-----------------------------------------------------------------------

 1400 CONTINUE
      CALL W3MISCAN(IUNIT,INLSF,INGBI,INGBD,LSAT,LPROD,LBRIT,NNALG,
     $ GBALG,(/KDATE(1),KDATE(2),KDATE(3),KDATE(5),KDATE(6)/),
     $ (/LDATE(1),LDATE(2),LDATE(3),LDATE(5),LDATE(6)/),IGNRTM,IBUFTN,
     $ IBDATE,IER)
      IF(IER.GT.1.OR.IER.LT.0)  THEN
C.......................................................................

C  ERROR READING A SCAN LINE - STOP 16
C  -----------------------------------

         PRINT 109, IUNIT,IER
         CALL W3TAGE('PREPOBS_PREPSSMI') 
         CALL ERREXIT(16)
C.......................................................................
      ELSE  IF(IER.EQ.0)  THEN

C=======================================================================
C                      WE HAVE A VALID SCAN
C=======================================================================

         IF(ISUPOB.EQ.0) CALL OPENMB(NFILE,SUBSET,IBDATE)

C  STORE THE SCAN TIME IN HOURS X 100
C  ----------------------------------

         ITIME = NINT((1000. * (REAL(IBUFTN(5)) + REAL(IBUFTN(6))/60. +
     $    REAL(IBUFTN(7))/3600.)) + 0.00005)

C  IF SUPEROBING, CHECK FOR CROSS OVER TO NEXT DAY (IF SO, ADD 24000
C   SO AVG. TIME MAKES SENSE; WILL CORRECT LATER IF TIME IS > 24000)
C  -----------------------------------------------------------------

         IF(ISUPOB.EQ.1.AND.ITMFL.AND.IBUFTN(5).LT.12) ITIME=ITIME+24000

C  CALL STORE1 TO CHECK FOR VALID RETRIEVAL FOR SELECTED PRODUCT(S)
C  CALL STORE2 TO CHECK FOR VALID RETRIEVAL FOR B. TEMPERATURES
C   IF SUPEROBING:     STORES DATA FROM EACH ORIG. RETR. INTO ARRAYS
C   IF NOT SUPEROBING: ENCODES ORIGINAL RETR. INTO OUTPUT BUFR FILE
C  -----------------------------------------------------------------

         IF(LPROD.OR.NNALG.OR.GBALG)  CALL STORE1(ITIME,NRF,IALG,NCEPRD)
         IF(IVAR(10).EQ.0)  CALL STORE2(ITIME)

C  GO BACK AND UNPACK NEXT SCAN LINE OF 64 RETRIEVALS
C  --------------------------------------------------

         GO TO 1400

      ELSE

C=======================================================================
C   ALL SCANS HAVE BEEN DECODED AND ALL RETRIEVALS STORED AS REQUESTED
C              (IER = 1 RETURNED FROM SUBROUTINE W3MISCAN)
C=======================================================================

C  IF NOT SUPEROBING ALL DONE
C  --------------------------

         IF(ISUPOB.EQ.1)  THEN

C  IF SUPEROBING ....
C  ------------------

            CALL OPENMG(NFILE,SUBOUT,IDATBF)

            IF(LPROD.OR.NNALG.OR.GBALG)  THEN

C   CALL SUPOB1 TO GENERATE SUPOBS FOR SELECTED PRODS & ENCODE IN BUFR
C   ------------------------------------------------------------------

               CALL SUPOB1

            ELSE

C   CALL SUPOB2 TO GENERATE SUPOBS FOR B. TEMPS & ENCODE IN BUFR
C   ------------------------------------------------------------

               CALL SUPOB2

            END IF
         END IF
      END IF

C=======================================================================

C  CLOSE OUTPUT BUFR FILE CONTAINING REPROCESSED SSM/I DATA
C  --------------------------------------------------------

      CALL UFBCNT(NFILE,IREC,ISUB)
      ISUBT = ISUBT + ISUB
      WRITE(6,1253) IREC,ISUB,ISUBT
 1253 FORMAT(/' --- WROTE OUTPUT BUFR DATA MSG NO.',I10,' WITH',I5,
     $ ' REPORTS (TOTAL NO. REPORTS WRITTEN =',I7,')')
      CALL CLOSBF(NFILE)
      WRITE(6,102) NFILE
  102 FORMAT(/5X,'===> OUTPUT BUFR DATA SET IN UNIT',I3,' SUCCESSFULLY',
     $ ' CLOSED'/)

      IF(IVAR(1).EQ.0)  THEN
         PRINT 656, LIMCNT,IQM,(NRF(I,1),I=0,4)
  656 FORMAT(/44X,'@@  W. SPEED RAIN FLAG CATEGORY COUNTS  @@'/1X,
     $ '(FOR ISUPOB=1 THIS INCLUDES ALL ORIGINAL RETR. EVEN THOSE NOT ',
     $ 'USED IN SUPEROBING DUE TO BEING IN BOXES WITH LESS THAN',I3,
     $ ' RETR.)'/40X,'(ALL CATEGORIES GREATER THAN',I3,' ARE NOT ',
     $ 'PROCESSED)'//
     $ 35X,'-->  CAT. 0, ERROR < 2 M/S  (ALL ALG.) .... ',I10,'  <--'/,
     $ 35X,'-->  CAT. 1, ERROR 2-5 M/S  (GOODBERLET)    '/,
     $ 35X,'             ERROR > 2 M/S  (NEURAL NET)... ',I10,'  <--'/,
     $ 35X,'-->  CAT. 2, ERROR 5-10 M/S (GOODBERLET) .. ',I10,'  <--'/,
     $ 35X,'-->  CAT. 3, ERROR > 10 M/S (GOODBERLET) .. ',I10,'  <--'/,
     $ 35X,'-->  CAT. IS MISSING ...................... ',I10,'  <--'/)
      END IF
      IF(IVAR(2).EQ.0.AND.QMPW)  THEN
         PRINT 658, LIMCNT,IQM,(NRF(I,2),I=0,4)
  658 FORMAT(/44X,'@@  P. WATER RAIN FLAG CATEGORY COUNTS  @@'/1X,
     $ '(FOR ISUPOB=1 THIS INCLUDES ALL ORIGINAL RETR. EVEN THOSE NOT ',
     $ 'USED IN SUPEROBING DUE TO BEING IN BOXES WITH LESS THAN',I3,
     $ ' RETR.)'/15X,'(ALL CATEGORIES GREATER THAN',I3,' ARE NOT ',
     $ 'PROCESSED, P. WATER W/ MISSING R. FLAG IS NEVER PROCESSED)'//
     $ 35X,'-->  CAT. 0, ERROR < 2 M/S  (ALL WSPD ALG.) ',I10,'  <--'/,
     $ 35X,'-->  CAT. 1, ERROR 2-5 M/S  (G-BERLET WSPD) '/,
     $ 35X,'             ERROR > 2 M/S  (N. NET WSPD) . ',I10,'  <--'/,
     $ 35X,'-->  CAT. 2, ERROR 5-10 M/S (G-BERLET WSPD) ',I10,'  <--'/,
     $ 35X,'-->  CAT. 3, ERROR > 10 M/S (G-BERLET WSPD) ',I10,'  <--'/,
     $ 35X,'-->  CAT. IS MISSING ...................... ',I10,'  <--'/)
      END IF

      IF(ISUPOB.EQ.1)  THEN

C  IF SUPEROBING WAS DONE, PRODUCE FINAL COUNTS OF ORIGINAL RETRIEVALS
C  -------------------------------------------------------------------

         PRINT 114, ITOBS,ITOBST
cdak     PRINT 115
cdak     PRINT 116, NOBS
      END IF
      PRINT 113, TEXT0(ISUPOB+1)
      PRINT 110, ICNT,ICNTT

      CALL W3TAGE('PREPOBS_PREPSSMI') 
      STOP

C-----------------------------------------------------------------------

  100 FORMAT(//20X,'*****  WELCOME TO THE SSM/I RETRIEVAL PROCESSOR: ',
     $ 'Y2K/F90 VERSION 20 AUG 2020  *****'/50X,'CENTER TIME:',I5,4I3/
     $ 31X,'TIME WINDOW IS +/-',I5,' (HOURS X 100 PLUS MINUTES) ABOUT ',
     $ 'CENTER TIME'///43X,'THE FOLLOWING PRODUCTS HAVE BEEN ',
     $ 'SPECIFIED:'/)
  101 FORMAT(48X,A34/)
 1101 FORMAT(22X,' * - SURFACE TAG, ICE CONCENTRATION, ICE AGE, ICE ',
     $ 'EDGE, CALCULATED SURFACE TYPE'/)
  106 FORMAT(/5X,'USER SPEC. SWITCHES: DELAT=',F5.1,'; DELON=',F5.1,
     $ '; LSAT=',10(L1,1X),'; ISUPOB=',I2/5X,'IVAR=',10I5,'; IQM=',I2,
     $ '; QMPW=',L1,'; LATITUDE BDRY:',I4,' TO',I4,' (S-N)'/5X,
     $ 'LONGITUDE BDRY:',I4,' TO',I4,' (W-E) DEG. W.'/5X,'LIMCNT=',I3,
     $ '; IALG=',I3/)
  107 FORMAT(23X,'LOOKING FOR SCANS WITHIN TIME LIMIT WINDOW ',I5,4I3,
     $ '  TO  ',I5,4I3,/)
  109 FORMAT(//' >>>>> ERROR ON UNIT',I3,' -- IER=',I8,' -- ABNORMAL ',
     $ 'STOP 16  <<<<<')
  110 FORMAT(40X,'>>>  OCEANIC SURFACE WIND SPEED . ',I10,'  <<<',/,
     $       40X,'>>>  OCEANIC PRECIPITABLE WATER . ',I10,'  <<<',/,
     $       40X,'>>>  RAINFALL RATE .............. ',I10,'  <<<',/,
     $       40X,'>>>  SURFACE TEMPERATURE ........ ',I10,'  <<<',/,
     $       40X,'>>>  OCEANIC CLOUD WATER ........ ',I10,'  <<<',/,
     $       40X,'>>>  SOIL MOISTURE .............. ',I10,'  <<<',/,
     $       40X,'>>>  SNOW DEPTH ................. ',I10,'  <<<',/,
     $       40X,'>>>  SEA-SURFACE TEMPERATURE .... ',I10,'  <<<',/,
     $       40X,'>>>  ADDITIONAL PRODUCTS ........ ',I10,'  <<<',/,
     $       40X,'>>>  BRIGHTNESS TEMP (7-CHANNEL). ',I10,'  <<<',//,
     $       40X,'>>>  TOTAL FOR ALL DATA TYPES ... ',I10,'  <<<',//,
     $       44X,'*****  PROGRAM SUCCESSFULLY COMPLETED  *****',/)
  113 FORMAT(/,44X,'***** ALL SSM/I ',A10,' PROCESSED *****',/)
  114 FORMAT(/,30X,'+++ NO. OF ORIGINAL RETRIEVALS THAT WERE USED TO G',
     $ 'ENERATE SUPEROBS +++'//45X,'OCEANIC SURFACE WIND SPEED . ',I10,
     $                        /45X,'OCEANIC PRECIPITABLE WATER . ',I10,
     $                        /45X,'RAINFALL RATE .............. ',I10,
     $                        /45X,'SURFACE TEMPERATURE ........ ',I10,
     $                        /45X,'OCEANIC CLOUD WATER ........ ',I10,
     $                        /45X,'SOIL MOISTURE .............. ',I10,
     $                        /45X,'SNOW DEPTH ................. ',I10,
     $                        /45X,'SEA-SURFACE TEMPERATURE .... ',I10,
     $                        /45X,'BRIGHTNESS TEMP (7-CHANNEL). ',I10,
     $                       //45X,'TOTAL FOR ALL DATA TYPES ... ',I10/)
  115 FORMAT(5X,'NOBS:')
  116 FORMAT(1X,30I4)
  118 FORMAT(//' >>>>> CANNOT SUPEROB "ADDITIONAL" PRODUCTS -- ',
     $ 'ABNORMAL STOP 18  <<<<<')
  119 FORMAT(//' >>>>> CANNOT PROCESS BOTH BRIGHTNESS TEMPERATURE AND ',
     $ 'ONE OR MORE PRODUCTS',
     $ ' -- ABNORMAL STOP 19  <<<<<')

C-----------------------------------------------------------------------

      END

C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    STORE1      UNPACKS SCANS, CHKS RETR, STORES PRODUCTS
C   PRGMMR: D. A. KEYSER     ORG: NP22       DATE: 1999-02-17
C
C ABSTRACT: UNPACKS THE 64 INDIVIDUAL RETRIEVALS OUT OF THE SCAN,
C   CHECKS RETRIEVALS AGAINST USER-SPECIFIED NAMELIST SWITCHES FOR
C   LAT/LON LIMITS, PRODUCT TYPE, AND "WORST" QUALITY MARK (FOR WIND
C   SPEED).  RETRIEVALS CONTAINING PRODUCTS WHICH PASS ALL CHECKS ARE
C   EITHER: STORED INTO XDATA ARRAY AND SENT TO SUBROUTINE OBUFRN FOR
C   ENCODING INTO OUTPUT BUFR FILE (IF NOT SUPEROBING); OR HAVE VALUE,
C   TIME, LATITUDE, LONGITUDE AND SUM OF SQUARES STORED INTO ARRAYS
C   BASED ON APPROPRIATE LAT/LON BOX (FOR LATER SUPEROBING).  THIS
C   SUBROUTINE HANDLES ONLY INPUT FROM PRODUCTS FILE, OR THE WIND
C   SPEED PRODUCT CALCULATED IN-LINE VIA GOODBERLET ALGORITHM FROM
C   BRIGHTNESS TEMPERATURES, OR THE WIND SPEED AND/OR TPW PRODUCT
C   CALCULATED IN- LINE VIA NEURAL NET ALGORITHM FROM BRIGHTNESS
C   TEMPERATURES.  SEE SUBROUTINE STORE2 FOR THE DIRECT PROCESSING OF
C   SEVEN-CHANNEL BRIGHTNESS TEMPERATURES.
C
C PROGRAM HISTORY LOG:
C 1993-01-21  D. A. KEYSER (NMC22) -- ORIGINAL AUTHOR
C 1993-07-21  D. A. KEYSER (NMC22) -- FOR INDIVIDUAL RETRIEVALS, ADDED
C        STORAGE OF ORIGINAL QUALITY MARKER (IN CHARACTER FORMAT IN
C        BYTE 2 OF CATEGORY 8 QUALITY MARKER WORD OF UNPACKED ON29
C        ARRAY); FOR SUPEROBS, STORES SUM OF SQUARES WITHIN EACH GRID
C        BOX (FOR LATER STANDARD DEVIATION CALCULATION)
C 1994-04-12  D. A. KEYSER (NMC22) -- MADE NECESSARY MODIFICATIONS TO
C        ALLOW SUBROUTINE TO COMPILE ON EITHER THE NAS OR THE CRAY
C 1994-09-06  D. A. KEYSER (NMC22) -- IN ORDER TO GET REGION SIZE DOWN
C        BELOW NAS LIMIT OF 9999K BYTES, COMBINED LATITUDE AND
C        LONGITUDE IN NEW STORAGE ARRAY "IPKDTA" (REPLACED "SUMLAT"
C        AND "SUMLON" STORAGE ARRAYS), EACH HELD IN 16-BITS; MODIFIED
C        TO ALLOW SUPEROBING OVER .5 DEG. BOXES, BUT ONLY IN NORTHERN
C        AND WESTERN HEMISPHERES
C 1994-10-05  D. A. KEYSER (NMC22) --  ADDED THE PRODUCTS CLOUD WATER,
C        SOIL MOISTURE AND SNOW DEPTH TO THE LIST OF PRODUCTS THAT CAN
C        BE PROCESSED; RENAMED TO 'STORE1' FROM 'STORE'
C 1995-01-05  D. A. KEYSER (NMC22) -- ADDED ABILITY TO PROCESS AND
C        -IF REQUESTED- SUPEROB WIND SPEED PRODUCTS THAT HAVE BEEN
C        GENERATED IN-LINE (VIA SUBR. W3FI86) USING EITHER THE
C        NEURAL NETWORK ALGORITHM OR THE GOODBERLET ALGORITHM.
C        THE ORIGINAL GOODBERLET WIND SPEED IN THE PRODUCTS DATA
C        SET(S) CAN ALSO STILL BE PROCESSED; NOTE HOWEVER THAT
C        ONLY ONE SOURCE OF WIND SPEED PRODUCT CAN BE CHOSEN IN A
C        SINGLE RUNNING OF THIS PROGRAM
C 1996-07-31  D. A. KEYSER (NP22) -- CAN NOW PROCESS OPERATIONAL FNOC
C        PRODUCTS ALONG WITH IN-LINE CALCULATED WIND SPEED PRODUCT;
C        ADDED N-LIST SWITCH "QMPW" WHICH, WHEN TRUE, WILL QUALITY
C        CONTROL TOTAL PRECIP. WATER PRODUCT AGAINST RAIN CONTAMINATION
C        FLAG AS IS DONE FOR WIND SPEED PRODUCT
C 1998-01-28  D. A. KEYSER (NP22) -- WITH CHANGE IN W3LIB ROUITNE
C        W3MISCAN, LATEST NN ALGORITHM (IALG=1) IS NN3 WHICH RETURNS
C        BOTH WIND SPEED AND TOTAL PRECIP. WATER PRODUCTS (UNLIKE NN2
C        WHICH RETURNED ONLY WIND SPEED), ALSO NN3 CAN ONLY RETURN
C        PRODUCTS WITH NO RAIN CONTAMINATION SO Q.M. HERE IS HARDWIRED
C        TO '0'
C 1999-02-17  D.A. KEYSER -- FOR NON-SUPEROB CASE, STORES DATA IN
C        XDATA ARRAY RATHER THAN UNPACKED ON29 ARRAY (OUTPUT FILE IS
C        NOW IN BUFR)
C
C USAGE:    CALL STORE1(ITIME,NRF,IALG,NCEPRD)
C   INPUT ARGUMENT LIST:
C     ITIME    - SCAN TIME (HOURS X 1000) (MAYBE PLUS 24000)
C     NRF      - (5,2) ARRAY HOLDING COUNTS FOR WIND SPEED (X,1) AND
C              - POSSIBLY TOTAL PRECIP. WATER (X,2) ACCORDING TO THE
C              - 4 RAIN FLAG CATEGORIES (PLUS MISSING RAIN FLAG), NOT
C              - INCLUDING ANY WIND SPEED/TOTAL PRECIP. WATER
C              - RETRIEVALS IN THIS SCAN
C     IALG     - INDICATOR ALGORITHM TYPE FOR WIND SPEED (=0 -
C              - WIND SPEED -IF SELECTED- OBTAINED FROM INPUT PRODUCTS
C              - FILE (EITHER FNOC OR NCEP); =1 - WIND SPEED IF
C              - SELECTED- AND - TPW -IF SELECTED- CALCULATED IN-
C              - LINE VIA NEURAL NET ALGORITHM FROM BRIGHTNESS
C              - TEMPERATURES; =2 - WIND SPEED -IF SELECTED- CALCULATED
C              - IN-LINE VIA GOODBERLET ALGORITHM FROM BRIGHTNESS
C              - TEMPERATURES)
C     NCEPRD   - LOGICAL, IF TRUE INDICATES THAT INPUT FILE TO THIS
C              - PROGRAM IS AN NCEP PRODUCTS FILE AND NOTHIS ELSE
C
C   OUTPUT ARGUMENT LIST:
C     NRF      - (5,2) ARRAY HOLDING COUNTS FOR WIND SPEED (X,1) AND
C              - POSSIBLY TOTAL PRECIP. WATER (X,2) ACCORDING TO THE
C              - 4 RAIN FLAG CATEGORIES (PLUS MISSING RAIN FLAG),
C              - INCLUDING ANY WIND SPEED/TOTAL PRECIP. WATER
C              - RETRIEVALS IN THIS SCAN
C
C   OUTPUT FILES:
C     UNIT 06  - STANDARD OUTPUT PRINT
C
C REMARKS: CALLED BY MAIN PROGRAM.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  WCOSS
C
C$$$
      SUBROUTINE STORE1(ITIME,NRF,IALG,NCEPRD)

      LOGICAL  QMPW,NCEPRD

      CHARACTER*5  TEXT2(8)
      CHARACTER*15 TEXT1(8)

      INTEGER  IDAT(27,64),L(8),M(2),N(0:2),NRF(0:4,2)

      REAL  SCALE(8),SCALE1(8)

      COMMON/SCAN/IBUFTN(1737)
      COMMON/KOUNT/ICNT(10),ICNTT,NFILE,IRECL,ISUBL,ISUBT
      COMMON/SWITCH/IVAR(10),IQM,ISUPOB,LATS,LATN,LONW,LONE,LIMCNT,QMPW
      COMMON/DGRID/LATSIZ,LONSIZ
      COMMON/BGRID/BLAT(360),BLON(720),NOBS(8,720,360)
      COMMON/SAVED/ITOBS(9),ITOBST,DGH,DGV,SUMDTA(8,720,360),
     $ SSQDTA(8,720,360),SUMTIM(8,720,360),SUMLAT(8,720,360),
     $ SUMLON(8,720,360)
      COMMON/REPT/XDATA(36),YDATA(3,7)

      EQUIVALENCE  (IDAT,IBUFTN(10))

      DATA  L/7,12,6,13,5,8,14,13/,M/24,26/,N/15,-99999,27/
      DATA  SCALE /10.,10.,1000000.,100.,100.,1000.,1000.,100./
      DATA  SCALE1/10.,10.,  278.,100.,100.,   1.,   1.,100./
      DATA  TEXT2/'M/SEC','MM   ','MM/HR','DEG K','MM   ','MM   ',
     $            'MM   ','DEG K'/
      DATA  TEXT1/'  WIND SPEED   ','  PREC. WATER  ','RAINFALL RATE  ',
     $            '  SFC. TEMP.   ','  CLOUD WATER  ','SOIL MOISTURE  ',
     $            '  SNOW DEPTH   ','SEA-SFC. TEMP. '/
      DATA BMISS/10E8/,IMISS/99999/

C***********************************************************************
C      LOOP THROUGH THE 64 RETRIEVALS THAT MAKE UP THE SCAN
C***********************************************************************

      LOOP1: DO IRTV = 1,64

C  STORE LAT. IN INLAT: INPUT AND OUTPUT: N(+), S(-) X 100
C  -------------------------------------------------------

         IF(IDAT(1,IRTV).GE.IMISS)  CYCLE LOOP1
         INLAT = IDAT(1,IRTV)

C  STORE LON. IN IWLON: INPUT: 0-360 E X 100; OUTPUT:  0-360 W X 100
C  STORE LON. IN IELON: INPUT AND OUTPUT: 0-360 E X 100
C  -----------------------------------------------------------------

         IF(IDAT(2,IRTV).GE.IMISS)  CYCLE LOOP1
         IWLON = MOD((36000-IDAT(2,IRTV)),36000)
         IELON = IDAT(2,IRTV)

C  CHECK THAT THIS RETRIEVAL IS WITHIN THE SPECIFIED LAT/LON BOUNDARY;
C   IF NOT, GO ON TO NEXT RETRIEVAL IN THE SCAN
C  -------------------------------------------------------------------

         IWLONT = IWLON
         LONWT  = LONW
         IF(LONW.LT.LONE)  THEN
            IF(IWLON.LT.LONE*100)  IWLONT = IWLON + 36000
            LONWT = LONW + 360
         END IF
         IF(INLAT.LT.LATS*100.OR.INLAT.GE.LATN*100.OR.IWLONT.GT.
     $    LONWT*100.OR.IWLONT.LE.LONE*100)  CYCLE LOOP1

         IGOOD = 0
         ILM = 0
         JLM = 0

         IF(NCEPRD)  THEN

C  FOR INPUT NCEP PROD. FILE (LATEST N.NET ALG.): RAIN FLAG ALWAYS 0
C  -----------------------------------------------------------------
            IRAINF = 0
         ELSE
            IRAINF = IDAT(N(IALG),IRTV)
         END IF

         IF(IALG.GT.0)  THEN

C  FOR IN-LINE WIND SPEED CALC.: TRANSFER WSPD TO LOCATION OF WSPD
C   FROM PROD. FILE, ALSO IF BARELY NEG. SET TO 0 SO IT'S PROCESSED
C  ----------------------------------------------------------------

            IDAT(7,IRTV) = IDAT(M(IALG),IRTV)
            IF(IDAT(7,IRTV).LT.0.AND.IDAT(7,IRTV).GT.-20) IDAT(7,IRTV)=0
            IF(IALG.EQ.1)  THEN

C  FOR IN-LINE TPW CALC.: TRANSF. TPW TO LOCATION OF TPW FROM PROD.
C   FILE FOR IALG=1: RAIN FLAG IS ALWAYS SET TO ZERO
C  ----------------------------------------------------------------

               IDAT(12,IRTV) = IDAT(25,IRTV)
               IRAINF = 0
            END IF
         END IF

C-----------------------------------------------------------------------
C LOOP THROUGH THE FIRST 8 POSSIBLE PRODUCT TYPES AS SPECIFIED BY USER
C-----------------------------------------------------------------------
         LOOP1n1: DO K = 1,8

C  CHECK FOR USER-SPECIFICATION AND VALID PRODUCT
C   (NOTE: MUST USE SURFACE TAG TO DIFFERENTIATE BETWEEN SST AND SURF.
C          TEMP. AS BOTH ARE RETURNED IN SAME LOCATION)
C  -------------------------------------------------------------------

            IF(IVAR(K).NE.0.OR.IDAT(L(K),IRTV).GE.IMISS)  CYCLE LOOP1n1
            IF(K.EQ.4)  THEN
               IF(IDAT(4,IRTV).EQ.5)  CYCLE LOOP1n1
            ELSE  IF(K.EQ.8)  THEN
               IF(IDAT(4,IRTV).NE.5)  CYCLE LOOP1n1
            END IF

C  ZDATA IS THE SELECTED PRODUCT VALUE SCALED TO BUFR UNITS:
C     FOR K = 1   ==> OCEANIC WIND SPEED (M/S)
C                     (FROM INPUT PRODUCTS FILE OR CALCULATED IN-LINE
C                      FROM INPUT B. TEMP FILE VIA EITHER NEURAL NET
C                      OR GOODBERLET ALGORITHM)
C     FOR K = 2   ==> OCEANIC TOTAL PRECIPITABLE WATER KG/(M**2) {MM}
C                     (FROM INPUT PRODUCTS FILE OR CALCULATED IN-LINE
C                      FROM INPUT B. TEMP FILE VIA NEURAL NET ALGORITHM)
C     FOR K = 3   ==> RAINFALL RATE KG /((M**2)*SEC) {MM/SEC}
C     FOR K = 4   ==> SURFACE TEMPERATURE (DEGREES KELVIN)
C     FOR K = 5   ==> OCEANIC CLOUD WATER KG/(M**2) {MM}
C     FOR K = 6   ==> SOIL MOISTURE (METERS)
C     FOR K = 7   ==> SNOW DEPTH (METERS)
C     FOR K = 8   ==> SEA-SURFACE TEMPERATURE (DEGREES KELVIN)

            ZDATA = IDAT(L(K),IRTV)/SCALE(K)

C  JDATA IS THE SELECTED PRODUCT VALUE AS READ IN FROM W3MISCAN
C   (DATA TYPE SAME AS FOR ZDATA, SEE W3MISCAN FOR UNITS)
C  ------------------------------------------------------------

            JDATA = IDAT(L(K),IRTV)

C  CHECK THAT THE UNPACKED PRODUCT VALUE IS A POSITIVE NUMBER
C  ----------------------------------------------------------

            IF(JDATA.LT.0)  THEN
               PRINT 101, TEXT1(K),REAL(JDATA)/SCALE1(K),TEXT2(K),
     $          IBUFTN(8),IBUFTN(9),INLAT,IWLON
               CYCLE LOOP1n1
            END IF
            IF(K.EQ.1.OR.(K.EQ.2.AND.QMPW))  THEN
               IF(IRAINF.LT.4)  THEN
                  NRF(IRAINF,K) = NRF(IRAINF,K) + 1
               ELSE
                  IF(K.EQ.1)  THEN
                     PRINT 567
                  ELSE
                     PRINT 568
                  END IF
  567 FORMAT(/1X,'+++ WIND SPEED RAIN FLAG IS MISSING, YET WIND SPEED ',
     $ 'PRODUCT IS NON-MISSING (SHOULD NEVER HAPPEN) - WIND SPEED NOT ',
     $ 'PROCESSED'/)
  568 FORMAT(/1X,'+++ WIND SPEED RAIN FLAG IS MISSING, YET P. WATER ',
     $ 'PRODUCT IS NON-MISSING (CAN HAPPEN?) - P. WATER IS NOT ',
     $ 'PROCESSED'/)
                  NRF(4,K) = NRF(4,K) + 1
               END IF

C  IF PROCESSING WIND SPEED OR PRECIP. WATER (AND QMPW IS TRUE FOR
C   LATTER): CHECK Q.M. AGAINST USER-SPECIFIED Q.M. LIMIT (NOTE: P.
C   WATER WITH MISSING Q.M. IS NEVER PROCESSED)
C  ----------------------------------------------------------------

               IF(IRAINF.GT.IQM)  CYCLE LOOP1n1
            END IF

C.......................................................................
C                    WE HAVE A VALID RETRIEVAL
C.......................................................................

            IF(ISUPOB.EQ.0)  THEN

C. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

C  IF NOT SUPEROBING, ENCODE ORIGINAL OBS INTO BUFR (AFTER INITALIZING
C   XDATA ARRAY AS MISSING)
C  -------------------------------------------------------------------

               XDATA = BMISS
               ICNT(K) = MIN(999999,ICNT(K)+1)
               ICNTT = ICNTT + 1

C  WRITE REPORT YEAR, MONTH, DAY TO XDATA(1), XDATA(2), XDATA(3), RESP.
C  WRITE REPORT HOUR, MIN, SEC TO XDATA(4), XDATA(5), XDATA(6), RESP.
C  --------------------------------------------------------------------
               XDATA(1:6)  = IBUFTN(2:7)

C  WRITE LAT. TO XDATA(7): INPUT: N(+), S(-) X 100; OUTPUT: N(+), S(-)
C  -------------------------------------------------------------------

               XDATA(7) = INLAT/100.

C  WRITE LON. TO XDATA(8): INPUT: 0-360 EAST X 100; OUTPUT: 0-360 EAST
C  -------------------------------------------------------------------

               XDATA(8) = IELON/100.

C  REPORT TYPE WILL BE WRITTEN TO XDATA(9) IN SUBR. OBUFRN (VARIES)
C  ----------------------------------------------------------------


C  WRITE NUMBER THAT WENT INTO AVERAGING TO XDATA(10) -- ALWAYS 1 HERE
C  -------------------------------------------------------------------

               XDATA(10) = 1

C  WRITE SATELLITE IDENTIFIER TO XDATA(11)
C  ---------------------------------------

               IF(IBUFTN(1).LT.IMISS)  XDATA(11) = IBUFTN(1)

C  WRITE ORBIT NUMBER TO XDATA(12)
C  -------------------------------

               IF(IBUFTN(9).LT.IMISS)  XDATA(12) = IBUFTN(9)

C  WRITE SCAN NUMBER TO XDATA(13)
C  ------------------------------

               IF(IBUFTN(8).LT.IMISS)  XDATA(13) = IBUFTN(8)

C  WRITE POSITION NUMBER TO XDATA(14)
C  ----------------------------------

               IF(IDAT(3,IRTV).LT.IMISS)  XDATA(14) = IDAT(3,IRTV)

C  WRITE PRODUCT VALUE TO XDATA(14+K) (SEE ABOVE FOR K)
C   (NOTE: AT THIS POINT, ZDATA CAN NEVER BE MISSING)
C  ----------------------------------------------------

               XDATA(14+K) = ZDATA

C  VALUES IN XDATA(23) THROUGH XDATA(30) APPLY ONLY TO SUPEROBS
C  ------------------------------------------------------------


C  WRITE RAIN FLAG (QUALITY MARKER) TO XDATA(31): (ONLY FOR SURFACE
C   WIND SPEED OR TOTAL PRECIPITABLE WATER, THE LATTER WHEN QMPW=T)
C  ----------------------------------------------------------------

               IF(IRAINF.LT.4)  THEN
                  IF(K.EQ.1.OR.(K.EQ.2.AND.QMPW))  XDATA(31) = IRAINF
               END IF

C  SUBROUTINE OBUFRN ENCODES THE OBS INTO OUTPUT BUFR FILE
C  -------------------------------------------------------

               CALL OBUFRN(K)

            ELSE

C. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

C  IF SUPEROBING, FIND LAT/LON BOX CONTAINING THIS RETRIEVAL (ILM, JLM)
C   (SINCE ALL PRODUCTS HAVE SAME LOCATION, THIS IS DONE ONLY FOR FIRST
C    REQUESTED PRODUCT THAT PASSES ALL OF THE CHECKS)
C  --------------------------------------------------------------------

               IF(IGOOD.EQ.0)  THEN
                  LATBEG = 1
                  KNDX = 0
                  LOOP1n2: DO II = LATBEG,LATSIZ
                     KNDX = KNDX + 1
                     IF(INLAT.GE.NINT((BLAT(KNDX)+DGV)*100.).OR.
     $                INLAT.LT.NINT((BLAT(KNDX)-DGV)*100.))CYCLE LOOP1n2
                     ILM = KNDX
                     LOOP1n3: DO IJ = 1,LONSIZ
                        IF(IWLON.GE.NINT((BLON(IJ)+DGH)*100.).OR.
     $                  IWLON.LT.NINT((BLON(IJ)-DGH)*100.))CYCLE LOOP1n3
                        JLM = IJ
                        GO TO 5
                     ENDDO LOOP1n3
                  ENDDO LOOP1n2

C  IF NO BOX FOUND, GO ON TO NEXT RETRIEVAL IN THE SCAN
C  ----------------------------------------------------

                  PRINT 105, INLAT,IWLON
                  CYCLE LOOP1
    5             CONTINUE
                  IGOOD = 1
               END IF

C  THE FOLLOWING SHOULD   N E V E R   HAPPEN!!
C  -------------------------------------------

               IF(ILM.EQ.0.OR.JLM.EQ.0)  THEN
                  PRINT *, '&&& EITHER ILM (',ILM,') OR JLM (',JLM,')',
     $             ' IS ZERO -- SHOULD NEVER HAPPEN !!!'
                  CYCLE LOOP1
               END IF

C  SUM UP VALUES WITHIN THE GRID BOX ILM, JLM
C  ------------------------------------------

               NOBS(K,JLM,ILM)   = NOBS(K,JLM,ILM) + 1
               SUMDTA(K,JLM,ILM) = SUMDTA(K,JLM,ILM) + JDATA
               SSQDTA(K,JLM,ILM) = SSQDTA(K,JLM,ILM) + REAL(JDATA*JDATA)
               SUMTIM(K,JLM,ILM) = SUMTIM(K,JLM,ILM) + REAL(ITIME)
               SUMLAT(K,JLM,ILM) = SUMLAT(K,JLM,ILM) + REAL(INLAT)
               SUMLON(K,JLM,ILM) = SUMLON(K,JLM,ILM) + REAL(IELON)

C. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

            END IF
         ENDDO LOOP1n1

C-----------------------------------------------------------------------

C  CALL SUBR. STORE3 IF "ADDITIONAL" PRODUCTS SPECIFIED BY USER
C  ------------------------------------------------------------

         IF(IVAR(9).EQ.0)  CALL STORE3(IRTV)

      ENDDO LOOP1

C***********************************************************************

  101 FORMAT(1X,'+++ NEG. ',A15,', SKIP:',F10.2,1X,A5,'; AT SCAN NO.',
     $ I8,'; ORBIT NO.',I8,'; LAT =',I7,' DEG. N; LON =',I7,' DEG. W')
  105 FORMAT(5X,'* * * *   RETRIEVAL LAT/LON IS EITHER MISSING OR ',
     $ 'INVALID, LAT=',I5,', LON=',I6,' -- GO ON TO NEXT RETRIEVAL IN ',
     $ 'THE SCAN'/)

      RETURN

      END

C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    STORE2      UNPACKS SCANS, CHKS RETR, STORES B. TEMPS 
C   PRGMMR: D. A. KEYSER     ORG: NP22       DATE: 1999-02-17
C
C ABSTRACT: UNPACKS THE 64 INDIVIDUAL RETRIEVALS OUT OF THE SCAN.
C   CHECKS RETRIEVALS AGAINST USER-SPECIFIED NAMELIST SWITCHES
C   FOR LAT/LON LIMITS.  RETRIEVALS CONTAINING BRIGHTNESS TEMPERATURE
C   WHICH PASS ALL CHECKS ARE EITHER: STORED INTO XDATA ARRAY AND SENT
C   TO SUBROUTINE OBUFRN FOR ENCODING INTO OUTPUT BUFR FILE (IF NOT
C   SUPEROBING); OR HAVE BRIGHTNESS TEMPERATURE, TIME, LATITUDE AND
C   LONGITUDE STORED INTO ARRAYS BASED ON APPROPRIATE LAT/LON BOX
C   (FOR LATER SUPEROBING).  THIS SUBROUTINE HANDLES ONLY THE SEVEN-
C   CHANNEL BRIGHTNESS TEMPERATURES.  SEE SUBROUTINE STORE1 FOR
C   PRODUCTS.
C
C PROGRAM HISTORY LOG:
C 1994-10-05  D. A. KEYSER (NMC22) --  ADAPTED FROM ORIGINAL
C        SUBROUTINE STORE, BUT NOW TO PROCESS THE SEVEN CHANNELS OF
C        BRIGHTNESS TEMPERATURES
C 1999-02-17  D.A. KEYSER -- FOR NON-SUPEROB CASE, STORES DATA IN
C        XDATA AND YDATA ARRAYS RATHER THAN UNPACKED ON29 ARRAY
C        (OUTPUT FILE IS NOW IN BUFR)
C
C USAGE:    CALL STORE2(ITIME)
C   INPUT ARGUMENT LIST:
C     ITIME    - SCAN TIME (HOURS X 1000) (MAYBE PLUS 24000)
C
C   OUTPUT FILES:
C     UNIT 06  - STANDARD OUTPUT PRINT
C
C REMARKS: CALLED BY MAIN PROGRAM.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  WCOSS
C
C$$$
      SUBROUTINE STORE2(ITIME)

      CHARACTER*15 TEXT1(7)

      INTEGER  IDAT(27,64)

      REAL  SUM(7),SSQ(7)

      COMMON/SCAN/IBUFTN(1737)
      COMMON/KOUNT/ICNT(10),ICNTT,NFILE,IRECL,ISUBL,ISUBT
      COMMON/SWITCH/IVAR(10),IQM,ISUPOB,LATS,LATN,LONW,LONE,LIMCNT,QMPW
      COMMON/DGRID/LATSIZ,LONSIZ
      COMMON/BGRID/BLAT(360),BLON(720),NOBS(8,720,360)
      COMMON/SAVED/ITOBS(9),ITOBST,DGH,DGV,SUMDTA(8,720,360),
     $ SSQDTA(8,720,360),SUMTIM(8,720,360),SUMLAT(8,720,360),
     $ SUMLON(8,720,360)
      COMMON/REPT/XDATA(36),YDATA(3,7)

      EQUIVALENCE  (IDAT,IBUFTN(10))

      DATA  TEXT1/' 19 GHZ V BT   ',' 19 GHZ H BT   ',' 22 GHZ V BT   ',
     $            ' 37 GHZ V BT   ',' 37 GHZ H BT   ',' 85 GHZ V BT   ',
     $            ' 85 GHZ H BT   '/
      DATA  BMISS/10E8/,IMISS/99999/

C***********************************************************************
C      LOOP THROUGH THE 64 RETRIEVALS THAT MAKE UP THE SCAN
C***********************************************************************

      LOOP1: DO IRTV = 1,64

C  STORE LAT. IN INLAT: INPUT AND OUTPUT: N(+), S(-) X 100
C  -------------------------------------------------------

         IF(IDAT(1,IRTV).GE.IMISS)  CYCLE LOOP1
         INLAT = IDAT(1,IRTV)

C  STORE LON. IN IWLON: INPUT: 0-360 E X 100; OUTPUT:  0-360 W X 100
C  STORE LON. IN IELON: INPUT AND OUTPUT: 0-360 E X 100
C  -----------------------------------------------------------------

         IF(IDAT(2,IRTV).GE.IMISS)  CYCLE LOOP1
         IWLON =  MOD((36000-IDAT(2,IRTV)),36000)
         IELON =  IDAT(2,IRTV)

C  CHECK THAT THIS RETRIEVAL IS WITHIN THE SPECIFIED LAT/LON BOUNDARY;
C   IF NOT, GO ON TO NEXT RETRIEVAL IN THE SCAN
C  -------------------------------------------------------------------

         IWLONT = IWLON
         LONWT  = LONW
         IF(LONW.LT.LONE)  THEN
            IF(IWLON.LT.LONE*100)  IWLONT = IWLON + 36000
            LONWT = LONW + 360
         END IF
         IF(INLAT.LT.LATS*100.OR.INLAT.GE.LATN*100.OR.IWLONT.GT.
     $    LONWT*100.OR.IWLONT.LE.LONE*100)  CYCLE LOOP1

         ILM = 1
         JLM = 1
         IF(ISUPOB.EQ.1)  THEN

C  IF SUPEROBING, FIND LAT/LON BOX CONTAINING THIS RETR. (ILM, JLM)
C  ----------------------------------------------------------------

            ILM = 0
            JLM = 0
            LATBEG = 1
            KNDX = 0
            LOOP1n1: DO II = LATBEG,LATSIZ
               KNDX = KNDX + 1
               IF(INLAT.GE.NINT((BLAT(KNDX)+DGV)*100.).OR.
     $            INLAT.LT.NINT((BLAT(KNDX)-DGV)*100.))  CYCLE LOOP1n1
               ILM = KNDX
               LOOP1n2: DO IJ = 1,LONSIZ
                  IF(IWLON.GE.NINT((BLON(IJ)+DGH)*100.).OR.
     $               IWLON.LT.NINT((BLON(IJ)-DGH)*100.))  CYCLE LOOP1n2
                  JLM = IJ
                  GO TO 5
               ENDDO LOOP1n2
            ENDDO LOOP1n1

C  IF NO BOX FOUND, GO ON TO NEXT RETRIEVAL IN THE SCAN
C  ----------------------------------------------------

            PRINT 105, INLAT,IWLON
            CYCLE LOOP1
    5       CONTINUE

C  THE FOLLOWING SHOULD   N E V E R   HAPPEN!!
C  -------------------------------------------

            IF(ILM.EQ.0.OR.JLM.EQ.0)  THEN
               PRINT *, '&&& EITHER ILM (',ILM,') OR JLM (',JLM,') IS',
     $          ' ZERO -- SHOULD NEVER HAPPEN !!!'
               CYCLE LOOP1
            END IF
         END IF

C-----------------------------------------------------------------------
C       LOOP THROUGH THE SEVEN CHANNELS OF BRIGHTNESS TEMPERATURES
C
C  THE SEVEN BRIGHTNESS TEMPERATURES STORED ARE:
C     FOR K = 1   ==> 19 GHZ V BRIGHTNESS TEMPERATURE (DEGREES KELVIN)
C     FOR K = 2   ==> 19 GHZ H BRIGHTNESS TEMPERATURE (DEGREES KELVIN)
C     FOR K = 3   ==> 22 GHZ V BRIGHTNESS TEMPERATURE (DEGREES KELVIN)
C     FOR K = 4   ==> 37 GHZ V BRIGHTNESS TEMPERATURE (DEGREES KELVIN)
C     FOR K = 5   ==> 37 GHZ H BRIGHTNESS TEMPERATURE (DEGREES KELVIN)
C     FOR K = 6   ==> 85 GHZ V BRIGHTNESS TEMPERATURE (DEGREES KELVIN)
C     FOR K = 7   ==> 85 GHZ H BRIGHTNESS TEMPERATURE (DEGREES KELVIN)
C
C    ONE REPORT WILL CONSIST OF THE SEVEN BRIGHTNESS TEMPERATURES
C-----------------------------------------------------------------------

         YDATA = BMISS

         DO K = 1,7

C  WRITE THE CHANNEL TO YDATA(1,K)
C  -------------------------------

            YDATA(1,K) = K

            IF(IDAT(K+16,IRTV).LT.IMISS)  THEN
               IF(IDAT(K+16,IRTV).GT.0)  THEN

C  COME HERE IF THE B. TEMP. FOR THIS CHN IS A NON-MISSING POS. NUMBER
C  -------------------------------------------------------------------

C   WRITE THE B. TEMP TO YDATA(2,K) WHERE K=1,7 IS DEFINED ABOVE
C   ------------------------------------------------------------

                  YDATA(2,K) = REAL(IDAT(K+16,IRTV))/100.

C   VALUES IN YDATA(3,K) WHERE K=1,7 APPLY ONLY TO SUPEROBS
C   -------------------------------------------------------


               ELSE
                  IF(ISUPOB.EQ.0)  THEN
                     PRINT 101, TEXT1(K),YDATA(2,K),IBUFTN(8),IBUFTN(9),
     $                INLAT,IWLON
                  ELSE
                     PRINT 102, TEXT1(K),YDATA(2,K),IBUFTN(8),IBUFTN(9),
     $                INLAT,IWLON
                     CYCLE LOOP1
                  END IF
               END IF
            END IF

C  SUM THE SEVEN B. TEMPS & THEIR SQUARES INTO SUM AND SSQ, RESP.
C   (FOR ISUPOB = 1 WILL BE USED LATER TO UPDATE SUMS IN GRID BOX)
C  ---------------------------------------------------------------

            SUM(K) = SUMDTA(K,JLM,ILM) + REAL(IDAT(K+16,IRTV))
            SSQ(K) = SSQDTA(K,JLM,ILM) +
     $       (REAL(IDAT(K+16,IRTV))*REAL(IDAT(K+16,IRTV)))

         ENDDO

C-----------------------------------------------------------------------

         IF(MAX(YDATA(2,1),YDATA(2,2),YDATA(2,3),YDATA(2,4),
     $    YDATA(2,5),YDATA(2,6),YDATA(2,7)).GT.BMISS-1..AND.ISUPOB.EQ.1)
     $    THEN

C  IF SUPEROBING, ALL SEVEN BRIGHTNESS TEMPERATURES MUST BE
C   NON-MISSING; IF NOT, GO ON TO NEXT RETRIEVAL IN THE SCAN
C  ---------------------------------------------------------

            PRINT 106, IBUFTN(8),IBUFTN(9),(YDATA(2,III),III=1,7)
            CYCLE LOOP1
         END IF

C-----------------------------------------------------------------------
C                    WE HAVE A VALID RETRIEVAL
C-----------------------------------------------------------------------

         IF(ISUPOB.EQ.0)  THEN

C.......................................................................

C  IF NOT SUPEROBING, ENCODE ORIGINAL OBS INTO BUFR (AFTER INITALIZING
C   XDATA ARRAY AS MISSING)
C  -------------------------------------------------------------------

            XDATA = BMISS
            ICNT(10) = MIN(999999,ICNT(10)+1)
            ICNTT = ICNTT + 1

C  WRITE REPORT YEAR, MONTH, DAY TO XDATA(1), XDATA(2), XDATA(3), RESP.
C  WRITE REPORT HOUR, MIN, SEC TO XDATA(4), XDATA(5), XDATA(6), RESP.
C  --------------------------------------------------------------------

            XDATA(1:6)  = IBUFTN(2:7)

C  WRITE LAT. TO XDATA(7): INPUT: N(+), S(-) X 100; OUTPUT: N(+), S(-)
C  -------------------------------------------------------------------

            XDATA(7)  = INLAT/100.

C  WRITE LON. TO XDATA(8): INPUT: 0-360 EAST X 100; OUTPUT: 0-360 EAST
C  -------------------------------------------------------------------

            XDATA(8)  = IELON/100.

C  WRITE REPORT TYPE TO XDATA(9) (SET = 68)
C  ----------------------------------------

            XDATA(9)  = 68.

C  WRITE NUMBER THAT WENT INTO AVERAGING TO XDATA(10) -- ALWAYS 1 HERE
C  -------------------------------------------------------------------

            XDATA(10) = 1

C  WRITE SATELLITE IDENTIFIER TO XDATA(11)
C  ---------------------------------------

            IF(IBUFTN(1).LT.IMISS)  XDATA(11) = IBUFTN(1)

C  WRITE ORBIT NUMBER TO XDATA(12)
C  -------------------------------

            IF(IBUFTN(9).LT.IMISS)  XDATA(12) = IBUFTN(9)

C  WRITE SCAN NUMBER TO XDATA(13)
C  ------------------------------

            IF(IBUFTN(8).LT.IMISS)  XDATA(13) = IBUFTN(8)

C  WRITE POSITION NUMBER TO XDATA(14)
C  ----------------------------------

            IF(IDAT(3,IRTV).LT.IMISS)  XDATA(14) = IDAT(3,IRTV)

C  {THE 7 CHN. NUMBERS AND B.TEMPS ALREADY WRITTEN TO YDATA(1,K) AND
C   YDATA(2,K), RESP. WHERE K=1,7}
C  -----------------------------------------------------------------


C  SUBROUTINE OBUFRN ENCODES THE OBS INTO OUTPUT BUFR FILE
C  -------------------------------------------------------

            CALL OBUFRN(10)

         ELSE

C-----------------------------------------------------------------------

C  IF SUPEROBING, SUM UP VALUES WITHIN THE GRID BOX ILM, JLM
C  ---------------------------------------------------------

            NOBS(1,JLM,ILM)     = NOBS(1,JLM,ILM) + 1
            SUMDTA(1:7,JLM,ILM) = SUM(1:7)
            SSQDTA(1:7,JLM,ILM) = SSQ(1:7)
            SUMTIM(1,JLM,ILM) = SUMTIM(1,JLM,ILM) + REAL(ITIME)
            SUMLAT(1,JLM,ILM) = SUMLAT(1,JLM,ILM) + REAL(INLAT)
            SUMLON(1,JLM,ILM) = SUMLON(1,JLM,ILM) + REAL(IELON)

C-----------------------------------------------------------------------

         END IF
      ENDDO LOOP1

C***********************************************************************

  101 FORMAT(' +++ NEG. ',A15,', SET TO MSG:',F10.2,' DEG K; AT SCAN ',
     $ 'NO.',I8,'; ORBIT NO.',I8,'; LAT =',I7,' DEG. N; LON =',I7,
     $ ' DEG. W')
  102 FORMAT(' +++ NEG. ',A15,':',F10.2,' DEG K; AT SCAN NO.',I8,
     $ '; ORBIT NO.',I8,'; LATAT LAT =',I7,' DEG. N; LON =',I7,' DEG. W'
     $ /15X,' -- GO ON TO NEXT RETRIEVAL IN THE SCAN'/)
  105 FORMAT(5X,'* * * *   RETRIEVAL LAT/LON IS EITHER MISSING OR ',
     $ 'INVALID, LAT=',I5,', LON=',I6,' -- GO ON TO NEXT RETRIEVAL IN ',
     $ 'THE SCAN'/)
  106 FORMAT(' +++ ONE OF THE SEVEN B.T.S IN THIS RETR. MISSING ',
     $ '(SUPEROBING) AT SCAN NO.',I8,'; ORBIT NO.',I8,'; GO TO NEXT ',
     $ 'RETR. IN SCAN'/15X,'BRIGHTNESS TEMPERATURES: ',7(I7,1X)/)

      RETURN

      END

C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    STORE3      STORES ADD'L PRODUCTS INTO XDATA ARRAY
C   PRGMMR: D. A. KEYSER     ORG: NP22       DATE: 1999-02-17
C
C ABSTRACT: STORES THE ADDITIONAL PRODUCTS INTO XDATA ARRAY ALONG
C   WITH REPORT'S LATITUDE, LONGITUDE AND TIME.  THEN CALLS
C   SUBROUTINE OBUFRN TO ENCODE REPORT INTO OUTPUT BUFR FILE.  ONLY
C   NON-MISSING PRODUCTS ARE STORED, UP TO FIVE POSSIBLE.
C
C PROGRAM HISTORY LOG:
C 1994-10-05  D. A. KEYSER (NMC22) --  ORIGINAL AUTHOR
C 1999-02-17  D.A. KEYSER -- STORES DATA IN XDATA ARRAY RATHER THAN
C        UNPACKED ON29 ARRAY (OUTPUT FILE IS NOW IN BUFR)
C
C USAGE:    CALL STORE3(IRTV)
C   INPUT ARGUMENT LIST:
C     IRTV     - INDEX POINTING TO RETRIEVAL NUMBER IN SCAN (1-64)
C
C   OUTPUT FILES:
C     UNIT 06  - STANDARD OUTPUT PRINT
C
C REMARKS: CALLED BY SUBROUTINE 'STORE1'.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  WCOSS
C
C$$$
      SUBROUTINE STORE3(IRTV)

      CHARACTER*5  TEXT2(5)
      CHARACTER*15 TEXT1(5)

      INTEGER  IDAT(27,64),L(5)

      COMMON/SCAN/IBUFTN(1737)
      COMMON/KOUNT/ICNT(10),ICNTT,NFILE,IRECL,ISUBL,ISUBT
      COMMON/REPT/XDATA(36),YDATA(3,7)
      EQUIVALENCE  (IDAT,IBUFTN(10))

      DATA  TEXT1/'  SURFACE TAG  ',' ICE CONCENT.  ','    ICE AGE    ',
     $            '    ICE EDGE   ',' CALC SFC TYP  '/
      DATA  TEXT2/' CF  ','  %  ',' CF  ',' CF  ',' CF  '/
      DATA  L/4,9,10,11,16/
      DATA  BMISS/10E8/,IMISS/99999/

      IWLON = MOD((36000-IDAT(2,IRTV)),36000)
      ITOT = 0

C***********************************************************************
C                LOOP THROUGH THE ADDITIONAL PRODUCTS
C           ONE REPORT CAN CONSIST OF UP TO FIVE PRODUCTS
C                   (MISSING PRODUCTS ARE NOT STORED)
C                     (NEVER ANY SUPEROBING HERE!!)
C***********************************************************************

      XDATA = BMISS

      DO K = 1,5

C  THE ADDITIONAL PRODUCTS STORED ARE:
C     FOR K=1  ==> SURFACE TAG (RANGE: 0,1,3-6)
C     FOR K=2  ==> ICE CONCENTRATION (PERCENT)
C     FOR K=3  ==> ICE AGE (RANGE: 0,1)
C     FOR K=4  ==> ICE EDGE (RANGE: 0,1)
C     FOR K=5  ==> CALCULATED SURFACE TYPE (RANGE: 1-20)

C  A MISSING PRODUCT IS NOT STORED
C  -------------------------------

         IF(IDAT(L(K),IRTV).GE.IMISS)  CYCLE
         IF(IDAT(L(K),IRTV).LT.0)  THEN

C  CHECK THAT THE UNPACKED PRODUCT IS A POS. NUMBER, IF NOT SKIP IT
C  ----------------------------------------------------------------

            PRINT 101, TEXT1(K),REAL(IDAT(L(K),IRTV)),TEXT2(K),
     $       IBUFTN(8),IBUFTN(9),IDAT(1,IRTV),IWLON
            CYCLE
         END IF

C  VALID PRODUCT: INCR. ITOT (# PACKED ADD'L DATA) BY 1 & STORE DATUM
C  ------------------------------------------------------------------

         ITOT = ITOT + 1
         XDATA(31+K) = REAL(IDAT(L(K),IRTV))
      ENDDO

C***********************************************************************

C  IF ALL PRODUCTS ARE MISSING, REPORT IS NOT STORED
C  -------------------------------------------------

      IF(ITOT.EQ.0)  GO TO 99
      ICNT(9) = MIN(999999,ICNT(9)+1)
      ICNTT = ICNTT + 1

C  WRITE REPORT YEAR, MONTH, DAY TO XDATA(1), XDATA(2), XDATA(3), RESP.
C  WRITE REPORT HOUR, MIN, SEC TO XDATA(4), XDATA(5), XDATA(6), RESP.
C  --------------------------------------------------------------------

      XDATA(1:6)  = IBUFTN(2:7)

C  WRITE LAT. TO XDATA(7): INPUT: N(+), S(-) X 100; OUTPUT: N(+), S(-)
C  -------------------------------------------------------------------

      XDATA(7)  = IDAT(1,IRTV)/100.

C  WRITE LON. TO XDATA(8): INPUT: 0-360 EAST X 100; OUTPUT: 0-360 EAST
C  -------------------------------------------------------------------

      XDATA(8)  = IDAT(2,IRTV)/100.

C  WRITE REPORT TYPE TO XDATA(9) (SET = 575 FOR ADD'L PRODUCTS)
C  ------------------------------------------------------------

      XDATA(9) = 575.

C  WRITE NUMBER THAT WENT INTO AVERAGING TO XDATA(10) -- ALWAYS 1 HERE
C  -------------------------------------------------------------------

      XDATA(10) = 1

C  WRITE SATELLITE IDENTIFIER TO XDATA(11)
C  ---------------------------------------

      IF(IBUFTN(1).LT.IMISS)  XDATA(11) = IBUFTN(1)

C  WRITE ORBIT NUMBER TO XDATA(12)
C  -------------------------------

      IF(IBUFTN(9).LT.IMISS)  XDATA(12) = IBUFTN(9)

C  WRITE SCAN NUMBER TO XDATA(13)
C  ------------------------------

      IF(IBUFTN(8).LT.IMISS)  XDATA(13) = IBUFTN(8)

C  WRITE POSITION NUMBER TO XDATA(14)
C  ----------------------------------

      IF(IDAT(3,IRTV).LT.IMISS)  XDATA(14) = IDAT(3,IRTV)

C  {THE 5 PRODUCTS ALREADY WRITTEN TO XDATA(31+K) WHERE K=1,5)}
C  ------------------------------------------------------------


C  SUBROUTINE OBUFRN ENCODES THE OBS INTO OUTPUT BUFR FILE
C  -------------------------------------------------------

      CALL OBUFRN(9)

   99 CONTINUE

  101 FORMAT(' +++ NEG. ',A15,':',F10.2,A5,'; AT SCAN NO.',I8,'; ORBIT',
     $ ' NO.',I8,'; LATAT LAT =',I7,' DEG. N; LON =',I7,' DEG. W'/15X,
     $ ' -- SKIP THIS PRODUCT IN PACKING THIS RETRIEVAL'/)

      RETURN

      END

C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    OBUFRN      ENCODES NON-SUPEROBED REPR. RPT INTO BUFR
C   PRGMMR: D. A. KEYSER     ORG: NP22       DATE: 1999-02-17
C
C ABSTRACT: ENCODES A SINGLE NON-SUPEROBED REPROCESSED SSM/I REPORT
C   (SUBSET) INTO BUFR THEN WRITES SUBSET INTO OUTPUT FILE.
C
C PROGRAM HISTORY LOG:
C 1999-02-17  D. A. KEYSER (NMC22) -- ORIGINAL AUTHOR (REPLACED
C        SUBROUTINE "PCKO29"
C
C USAGE:    CALL OBUFRN(K)
C   INPUT ARGUMENT LIST:
C     K        - PROD/B.TEMP INDICATOR (=1 -OCEANIC SFC. WIND SPEED;
C                =2 -OCEANIC PRECIP. WATER; =3 -RAINFALL RATE;
C                =4 -SURFACE TEMP.; =5 -OCEANIC CLOUD WATER;  =6 -SOIL
C                MOISTURE; =7 -SNOW DEPTH; =8 -SEA-SFC TEMP; =9 -FIVE
C                "ADDITIONAL" PRODUCTS; =10 -SEVEN-CHANNEL BRIGHTNESS
C                TEMPERATURE)
C
C   OUTPUT FILES:
C     UNIT 06  - STANDARD OUTPUT PRINT
C     UNIT 51  - NCEP REPROCESSED SSM/I BUFR FILE CONTAINING
C                INDIVIDUAL NON-SUPEROBED RETRIEVALS
C
C REMARKS: CALLED BY SUBROUTINES 'STORE1' AND 'STORE2'.
C
C
C  SUBROUTINE OBUFRN WILL PACK THE REPROCESSED NON-SUPEROBED SSM/I
C  REPORT INTO THE BUFR DATABASE. LAYOUT OF THE ARRAY'S XDATA AND
C  YDATA CONTAINING THE SSM/I REPORT ARE AS FOLLOWS:
C  (NOTE: NOT ALL VALUES ARE ENCODED, DEPENDS UPON THE TYPE OF REPORT
C         THAT HAS BEEN PASSED INTO THIS SUBROUTINE)
C
C  --> ALL REPORTS GET ALL OF THE FOLLOWING DATA ENCODED:
C
C RPID  001198   REPORT IDENTIFIER (CHARACTER*8) ........... STNID
C YEAR  004001   YEAR ...................................... XDATA(1)
C MNTH  004002   MONTH ..................................... XDATA(2)
C DAYS  004003   DAY ....................................... XDATA(3)
C HOUR  004004   HOUR ...................................... XDATA(4)
C MINU  004005   MINUTE .................................... XDATA(5)
C SECO  004005   SECOND .................................... XDATA(6)
C CLAT  005002   LATITUDE .................................. XDATA(7)
C CLON  006002   LONGITUDE ................................. XDATA(8)
C NMCT  055006   NCEP REPORT TYPE .......................... XDATA(9)
C ACAV  008022   TOTAL NUMBER (WITH RESPECT TO AVERAGE) .... XDATA(10)
C SAID  001007   SATELLITE IDENTIFIER ...................... XDATA(11)
C ORBN  005040   ORBIT NUMBER .............................. XDATA(12)
C SCNN  005201   SCAN NUMBER ............................... XDATA(13)
C POSN  005202   POSITION NUMBER ........................... XDATA(14)
C
C  --> SINGLE PRODUCT WIND SPEED GETS ALL OF THE FOLLOWING ENCODED:
C
C WSPD  011002   WIND SPEED (OCEAN SURFACE) ................ XDATA(15)
C RFLG  033217   RAIN FLAG (WIND QUALITY INDICATOR) ........ XDATA(31)
C
C  --> SINGLE PRODUCT TOTAL PRECIPITABLE WATER GETS ALL OF THE
C       FOLLOWING ENCODED:
C
C TPWT  013016   TOTAL PRECIPITABLE WATER .................. XDATA(16)
C RFLG  033217   RAIN FLAG (WIND QUALITY INDICATOR) ........ XDATA(31)
C
C  --> SINGLE PRODUCT RAINFALL RATE GETS ALL OF THE FOLLOWING ENCODED:
C
C REQV  013014   RAINFALL RATE ............................. XDATA(17)
C
C  --> SINGLE PRODUCT SKIN TEMPERATURE GETS ALL OF THE FOLLOWING
C       ENCODED:
C
C TMSK  012161   SKIN TEMPERATURE (SURFACE) ................ XDATA(18)
C
C  --> SINGLE PRODUCT CLOUD WATER GETS ALL OF THE FOLLOWING ENCODED:
C
C METFET 008001  METEOROLOGICAL FEATURE ....................    12 (cld)
C VILWC 021031   VERTICALLY-INTEGRATED LIQUID WATER CONTENT. XDATA(19)
C METFET 008001  METEOROLOGICAL FEATURE .................... misg (cncl)
C
C  --> SINGLE PRODUCT SOIL MOISTURE GETS ALL OF THE FOLLOWING ENCODED:
C
C SMOI  013197   SOIL MOISTURE ............................. XDATA(20)
C
C  --> SINGLE PRODUCT SNOW DEPTH GETS ALL OF THE FOLLOWING ENCODED:
C
C TOSD  013013   SNOW DEPTH ................................ XDATA(21)
C
C  --> SINGLE PRODUCT SEA-SURFACE TEMPERATURE GETS ALL OF THE FOLLOWING
C       ENCODED:
C
C SST1  022043   SEA SURFACE TEMPERATURE ................... XDATA(22)
C
C  --> 5 ADDITIONAL PRODUCT REPORTS GET ALL OF THE FOLLOWING DATA
C       ENCODED:
C
C SFTG  002217   SURFACE TAG ............................... XDATA(32)
C ICON  020208   ICE CONCENTRATION ......................... XDATA(33)
C ICAG  020209   ICE AGE ................................... XDATA(34)
C ICED  020210   ICE EDGE .................................. XDATA(35)
C SFTP  020216   CALCULATED SURFACE TYPE ................... XDATA(36)
C  (NOTE: ONE OR MORE OF THESE PRODUCTS MAY BE MISSING FOR A REPORT)
C
C  --> BRIGHTNESS TEMPERATURE REPORTS GET ALL OF THE FOLLOWING DATA
C       ENCODED:
C
C BELOW, Y RANGES FROM 1-7 WHERE:
C   Y = 1 -- 19 GHZ VERTICAL CHANNEL
C       2 -- 19 GHZ HORIZONTAL CHANNEL
C       3 -- 22 GHZ VERTICAL CHANNEL
C       4 -- 37 GHZ VERTICAL CHANNEL
C       5 -- 37 GHZ HORIZONTAL CHANNEL
C       6 -- 85 GHZ VERTICAL CHANNEL
C       7 -- 85 GHZ HORIZONTAL CHANNEL
C
C CHNM  005042   CHANNEL NUMBER ............................ YDATA(1,Y)
C TMBR  012063   BRIGHTNESS TEMPERATURE .................... YDATA(2,Y)
C
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  WCOSS
C
C$$$
      SUBROUTINE OBUFRN(K)

      CHARACTER*1  CSAT(240:249),CPRD(10)
      CHARACTER*8  STNID
      CHARACTER*5  CPROD(8)

      REAL  RPTYP(8)
      REAL(8)  HDR_8(15),PROD_8,APROD_8(5),BTMP_8(2,7),METFET_8(2)

      COMMON/SCAN/IBUFTN(1737)
      COMMON/KOUNT/ICNT(10),ICNTT,NFILE,IRECL,ISUBL,ISUBT
      COMMON/REPT/XDATA(36),YDATA(3,7)

      EQUIVALENCE (STNID,HDR_8(1))

      DATA CPROD  /'WSPD ','TPWT ','REQV ','TMSK ','VILWC','SMOI ',
     $             'TOSD ','SST1 '/
      DATA  RPTYP/571.,65.,66.,576.,69.,573.,574.,577./

C          IVAR:    1   2   3   4   5   6   7   8   9  10
      DATA CPRD   /'A','E','I','M','Q','S','U','Z','W','Y'/

      DATA CSAT
C                           **  DMSP  **
C --------------- 240  241  242  243  244  245  246  247  248  249
     $           /'A', 'B', 'C', 'D', 'A', 'B', 'C', 'D', 'A', 'B' /

      DATA  BMISS/10E8/

      IF(K.LT.9)  THEN

C***********************************************************************
C              COME HERE FOR INPUT FROM PRODUCTS FILE OR 
C   WIND SPEED PRODUCT CALCULATED IN-LINE VIA GOODBERLET OR NEURAL NET
C                 ALGORITHM FROM INPUT BRIGHTNESS TEMPERATURES OR
C     TOTAL PRECIP. WATER PRODUCT CALCULATED IN-LINE VIA NEURAL NET
C                  ALGORITHM FROM INPUT BRIGHTNESS TEMPERATURES
C    {BUT NOT FOR THE "ADDITIONAL" PRODUCTS IN IVAR(9) OR FOR THE 
C                 BRIGHTNESS TEMPERATURES IN IVAR(10)}
C***********************************************************************

C  WRITE REPORT TYPE TO XDATA(9):
C   FOR OCEANIC WIND SPEED           = 571
C   FOR OCEANIC PRECIPITABLE WATER   =  65
C   FOR RAINFALL RATE (LAND & OCEAN) =  66
C   FOR SURFACE TEMPERATURE          = 576
C   FOR OCEANIC CLOUD WATER          =  69
C   FOR SOIL MOISTURE                = 573
C   FOR SNOW DEPTH                   = 574
C   FOR SEA-SURFACE TEMPERATURE      = 577

         XDATA(9) = RPTYP(K)
      END IF

C***********************************************************************
C   COME HERE FOR ALL DATA TYPES (PRODUCTS OR BRIGHTNESS TEMPERATURES)
C***********************************************************************

C  STNID HOLDS STATION IDENTIFICATION
C    -- POSITION 1 HOLDS DMSP SATELLITE INDICATOR
C        SAT. NO. 240 (F-07), 244 (F-11), 248 (F-15) ........... 'A'
C        SAT. NO. 241 (F-08), 245 (F-12), 249 (future) ......... 'B'
C        SAT. NO. 242 (F-09), 246 (F-13) ....................... 'C'
C        SAT. NO. 243 (F-10), 247 (F-14) ....................... 'D'
C        NOTE: DEPENDING UPON VALUE OF "LSAT" SWITCH, TWO DIFFERENT
C              SATELLITES COULD GET SAME CHARACTER HERE (E.G.,
C              IF LSAT SELECTED F-11 AND F-15, ALL REPORTS WOULD
C              BEGIN WITH 'A')
C    -- POSITION 8 HOLDS SSM/I DATA TYPE
C        OCEANIC WIND SPEED .................................... 'A'
C        OCEANIC PRECIPITABLE WATER ............................ 'E'
C        RAINFALL RATE ......................................... 'I'
C        SURFACE TEMPERATURE ................................... 'M'
C        OCEANIC CLOUD WATER ................................... 'Q'
C        SOIL MOISTURE ......................................... 'S'
C        SNOW DEPTH ............................................ 'U'
C        SEA-SURFACE TEMPERATURE ............................... 'Z'
C        FIVE "ADDITIONAL" PRODUCTS ............................ 'W'
C        SEVEN-CHANNEL BRIGHTNESS TEMPERATURES ................. 'Y'
C    -- POSITIONS 2-7 HOLD THE SEQUENTIAL SERIAL INDEX (000001 - 999999)
C        (UNIQUE FOR EACH DATA TYPE)

      STNID(1:1) = CSAT(IBUFTN(1))
      WRITE(STNID(2:7),'(I6.6)')  ICNT(K)
      STNID(8:8) = CPRD(K)


C  ENCODE HEADER INFORMATION (SAME FOR ALL TYPES) INTO SUBSET (REPORT)
C  -------------------------------------------------------------------

      HDR_8(2:15) = XDATA(1:14)
      CALL UFBINT(NFILE,HDR_8(1:11),11,1,IRET,
     $ 'RPID YEAR MNTH DAYS HOUR MINU SECO CLAT CLON NMCT ACAV')


C  ENCODE THE SATELLITE IDENTIFIER, ORBIT NUMBER, SCAN NUMBER AND
C   POSITION NUMBER FOR THIS RETRIEVAL
C  --------------------------------------------------------------

      CALL UFBINT(NFILE,HDR_8(12:15),4,1,IRET,'SAID ORBN SCNN POSN')

      IF(K.LT.9)  THEN

C  ENCODE THE SINGLE RETRIEVAL PRODUCT DATA INTO SUBSET (REPORT)
C   (THE PRODUCT IS BASED ON THE VALUE OF K)
C  -------------------------------------------------------------

         PROD_8 = XDATA(14+K)
         CALL UFBINT(NFILE,PROD_8,1,1,IRET,CPROD(K))

C  ENCODE THE RAIN FLAG (QUALITY INDICATOR), BUT ONLY FOR WIND SPEED
C   OR PRECIPITABLE WATER REPORTS
C  -----------------------------------------------------------------

         IF(K.LT.3)  THEN
            PROD_8 = XDATA(31)
            CALL UFBINT(NFILE,PROD_8,1,1,IRET,'RFLG')
         END IF

C  ENCODE METEOROLOGICAL FEATURE (FIRST AS 12, CLOUD; AGAIN AS
C   MISSING, CANCELLED), BUT ONLY FOR CLOUD WATER RPEORTS
C  -----------------------------------------------------------

         IF(K.EQ.5)  THEN
            METFET_8(1) = 12
            METFET_8(2) = BMISS
            CALL UFBREP(NFILE,METFET_8,1,2,IRET,'METFET')
         END IF

      ELSE  IF(K.EQ.9)  THEN

C  ENCODE THE 5 ADDITIONAL PRODUCTS INTO SUBSET (REPORT)
C  -----------------------------------------------------

         APROD_8 = XDATA(32:36)
         CALL UFBINT(NFILE,APROD_8,5,1,IRET,'SFTG ICON ICAG ICED SFTP')

      ELSE  IF(K.EQ.10)  THEN

C  ENCODE THE RADIANCE DATA INTO SUBSET (REPORT)
C  ---------------------------------------------

         BTMP_8(1,1:7) = YDATA(1,1:7)
         BTMP_8(2,1:7) = YDATA(2,1:7)
         CALL UFBINT(NFILE,BTMP_8,2,7,IRET,'CHNM TMBR')

      END IF

C  WRITE THE ENCODED SUBSET (REPORT) INTO THE OUTPUT BUFR MESSAGE
C  --------------------------------------------------------------

      CALL WRITSB(NFILE)

      CALL UFBCNT(NFILE,IREC,ISUB)
      IF(IREC.GT.IRECL)  THEN
         ISUBT = ISUBT + ISUBL
         WRITE(6,1254) IREC-1,ISUBL,ISUBT
 1254 FORMAT(/' --- THIS REPORT OPENS NEW OUTPUT BUFR MSG: LAST MSG ',
     $ 'WAS NO.',I10,' (DATA) WITH',I5,' RPTS (TOTAL NO. RPTS WRITTEN=',
     $ I7,')')
      END IF

      ISUBL = ISUB
      IRECL = IREC

      RETURN

      END

C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    OBUFRS      ENCODES SUPEROBED REPR. REPORT INTO BUFR
C   PRGMMR: D. A. KEYSER     ORG: NP22       DATE: 1999-02-17
C
C ABSTRACT: ENCODES A SINGLE SUPEROBED REPROCESSED SSM/I REPORT
C   (SUBSET) INTO BUFR THEN WRITES SUBSET INTO OUTPUT FILE.
C
C PROGRAM HISTORY LOG:
C 1999-02-17  D. A. KEYSER (NMC22) -- ORIGINAL AUTHOR (REPLACED
C        SUBROUTINE "PCKO29"
C
C USAGE:    CALL OBUFRS(K)
C   INPUT ARGUMENT LIST:
C     K        - PROD/B.TEMP INDICATOR (=1 -OCEANIC SFC. WIND SPEED;
C                =2 -OCEANIC PRECIP. WATER; =3 -RAINFALL RATE;
C                =4 -SURFACE TEMP.; =5 -OCEANIC CLOUD WATER;  =6 -SOIL
C                MOISTURE; =7 -SNOW DEPTH; =8 -SEA-SFC TEMP;
C                =10 -SEVEN-CHANNEL BRIGHTNESS TEMPERATURE)
C
C   OUTPUT FILES:
C     UNIT 06  - STANDARD OUTPUT PRINT
C     UNIT 51  - NCEP REPROCESSED SSM/I BUFR FILE CONTAINING
C                INDIVIDUAL SUPEROBED RETRIEVALS
C
C REMARKS: CALLED BY SUBROUTINES 'SUPOB1' AND 'SUPOB2'.
C
C
C  SUBROUTINE OBUFRS WILL PACK THE REPROCESSED SUPEROBED SSM/I REPORT
C  INTO THE BUFR DATABASE. LAYOUT OF THE ARRAY'S XDATA AND YDATA
C  CONTAINING THE SSM/I REPORT ARE AS FOLLOWS:
C  (NOTE: NOT ALL VALUES ARE ENCODED, DEPENDS UPON THE TYPE OF REPORT
C         THAT HAS BEEN PASSED INTO THIS SUBROUTINE)
C
C  --> ALL REPORTS GET ALL OF THE FOLLOWING DATA ENCODED:
C
C RPID  001198   REPORT IDENTIFIER (CHARACTER*8) ........... STNID
C YEAR  004001   YEAR ...................................... XDATA(1)
C MNTH  004002   MONTH ..................................... XDATA(2)
C DAYS  004003   DAY ....................................... XDATA(3)
C HOUR  004004   HOUR ...................................... XDATA(4)
C MINU  004005   MINUTE .................................... XDATA(5)
C SECO  004005   SECOND .................................... XDATA(6)
C CLAT  005002   LATITUDE .................................. XDATA(7)
C CLON  006002   LONGITUDE ................................. XDATA(8)
C NMCT  055006   NCEP REPORT TYPE .......................... XDATA(9)
C ACAV  008022   TOTAL NUMBER (WITH RESPECT TO AVERAGE) .... XDATA(10)
C
C NOTE: THE CODE TABLE OF "FOST" (FIRST ORDER STATISTIC) IS AS
C       FOLLOWS:
C               =  4 - MEAN VALUE
C               = 10 - STANDARD DEVIATION VALUE
C
C  --> SINGLE PRODUCT WIND SPEED GETS ALL OF THE FOLLOWING ENCODED:
C
C FOST  008023   FIRST ORDER STATISTIC .....................     4
C WSPD  011002   OCEAN SURFACE WIND SPEED (MEAN) ........... XDATA(15)
C FOST  008023   FIRST ORDER STATISTIC .....................    10
C WSPD  011002   OCEAN SURFACE WIND SPEED (STANDARD DEV.) .. XDATA(23)
C
C  --> SINGLE PRODUCT TOTAL PRECIPITABLE WATER GETS ALL OF THE
C       FOLLOWING ENCODED:
C
C FOST  008023   FIRST ORDER STATISTIC .....................     4
C TPWT  013016   TOTAL PRECIPITABLE WATER (MEAN) ........... XDATA(16)
C FOST  008023   FIRST ORDER STATISTIC .....................    10
C TPWT  013016   TOTAL PRECIPITABLE WATER (STANDARD DEV.) .. XDATA(24)
C
C  --> SINGLE PRODUCT RAINFALL RATE GETS ALL OF THE FOLLOWING ENCODED:
C
C FOST  008023   FIRST ORDER STATISTIC .....................     4
C REQV  013014   RAINFALL RATE (MEAN) ...................... XDATA(17)
C FOST  008023   FIRST ORDER STATISTIC .....................    10
C REQV  013014   RAINFALL RATE (STANDARD DEVIATION) ........ XDATA(25)
C
C  --> SINGLE PRODUCT SKIN TEMPERATURE GETS ALL OF THE FOLLOWING
C       ENCODED:
C
C FOST  008023   FIRST ORDER STATISTIC .....................     4
C TMSK  012161   SKIN TEMPERATURE (SURFACE) (MEAN) ......... XDATA(18)
C FOST  008023   FIRST ORDER STATISTIC .....................    10
C TMSK  012161   SKIN TEMPERATURE (SURFACE) (STANDARD DEV.). XDATA(26)
C
C  --> SINGLE PRODUCT CLOUD WATER GETS ALL OF THE FOLLOWING ENCODED:
C
C FOST  008023   FIRST ORDER STATISTIC .....................     4
C METFET 008001  METEOROLOGICAL FEATURE ....................    12 (cld)
C VILWC 021031   VERT.-INTEGR. LIQUID WATER CONTENT (MEAN) . XDATA(19)
C METFET 008001  METEOROLOGICAL FEATURE .................... misg (cncl)
C FOST  008023   FIRST ORDER STATISTIC .....................    10
C METFET 008001  METEOROLOGICAL FEATURE ....................    12 (cld)
C VILWC 021031   CLOUD WATER (STANDARD DEVIATION) .......... XDATA(27)
C METFET 008001  METEOROLOGICAL FEATURE .................... misg (cncl)
C
C  --> SINGLE PRODUCT SOIL MOISTURE GETS ALL OF THE FOLLOWING ENCODED:
C
C FOST  008023   FIRST ORDER STATISTIC .....................     4
C SMOI  013197   SOIL MOISTURE (MEAN) ...................... XDATA(20)
C FOST  008023   FIRST ORDER STATISTIC .....................    10
C SMOI  013197   SOIL MOISTURE (STANDARD DEVIATION) ........ XDATA(28)
C
C  --> SINGLE PRODUCT SNOW DEPTH GETS ALL OF THE FOLLOWING ENCODED:
C
C FOST  008023   FIRST ORDER STATISTIC .....................     4
C TOSD  013013   SNOW DEPTH (MEAN) ......................... XDATA(21)
C FOST  008023   FIRST ORDER STATISTIC .....................    10
C TOSD  013013   SNOW DEPTH (STANDARD DEVIATION) ........... XDATA(29)
C
C  --> SINGLE PRODUCT SEA-SURFACE TEMPERATURE GETS ALL OF THE FOLLOWING
C       ENCODED:
C
C FOST  008023   FIRST ORDER STATISTIC .....................     4
C SST1  022043   SEA-SURFACE TEMPERATURE (MEAN) ............ XDATA(22)
C FOST  008023   FIRST ORDER STATISTIC .....................    10
C SST1  022043   SEA-SURFACE TEMPERATURE (STANDARD DEV.) ... XDATA(30)
C
C  --> BRIGHTNESS TEMPERATURE REPORTS GET ALL OF THE FOLLOWING DATA
C       ENCODED:
C
C BELOW, Y RANGES FROM 1-7 WHERE:
C   Y = 1 -- 19 GHZ VERTICAL CHANNEL
C       2 -- 19 GHZ HORIZONTAL CHANNEL
C       3 -- 22 GHZ VERTICAL CHANNEL
C       4 -- 37 GHZ VERTICAL CHANNEL
C       5 -- 37 GHZ HORIZONTAL CHANNEL
C       6 -- 85 GHZ VERTICAL CHANNEL
C       7 -- 85 GHZ HORIZONTAL CHANNEL
C
C CHNM  005042   CHANNEL NUMBER ............................ YDATA(1,Y)
C FOST  008023   FIRST ORDER STATISTIC .....................     4
C TMBR  012063   BRIGHTNESS TEMPERATURE (MEAN) ............. YDATA(2,Y)
C FOST  008023   FIRST ORDER STATISTIC .....................    10
C TMBR  012063   BRIGHTNESS TEMPERATURE (STANDARD DEV.) .... YDATA(3,Y)
C
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  WCOSS
C
C$$$
      SUBROUTINE OBUFRS(K)

      CHARACTER*1  CPRD(10)
      CHARACTER*8  STNID
      CHARACTER*10 CPROD(8)

      REAL  RPTYP(8)
      REAL(8)  HDR_8(15),PROD_8(2,2),BTMP_8(2,2,7),CHNM_8(7),
     $ METFET_8(4)

      COMMON/KOUNT/ICNT(10),ICNTT,NFILE,IRECL,ISUBL,ISUBT
      COMMON/REPT/XDATA(36),YDATA(3,7)

      EQUIVALENCE (STNID,HDR_8(1))

      DATA CPROD  /'FOST WSPD ','FOST TPWT ','FOST REQV ','FOST TMSK ',
     $             'FOST VILWC','FOST SMOI ','FOST TOSD ','FOST SST1 '/
      DATA  RPTYP/571.,65.,66.,576.,69.,573.,574.,577./

C          IVAR:    1   2   3   4   5   6   7   8   9  10
      DATA CPRD   /'A','E','I','M','Q','S','U','Z','W','Y'/

      DATA  BMISS/10E8/

      IF(K.LT.9)  THEN

C***********************************************************************
C              COME HERE FOR INPUT FROM PRODUCTS FILE OR 
C   WIND SPEED PRODUCT CALCULATED IN-LINE VIA GOODBERLET OR NEURAL NET
C                 ALGORITHM FROM INPUT BRIGHTNESS TEMPERATURES OR
C     TOTAL PRECIP. WATER PRODUCT CALCULATED IN-LINE VIA NEURAL NET
C                  ALGORITHM FROM INPUT BRIGHTNESS TEMPERATURES
C    {BUT NOT FOR THE "ADDITIONAL" PRODUCTS IN IVAR(9) OR FOR THE 
C                 BRIGHTNESS TEMPERATURES IN IVAR(10)}
C***********************************************************************

C  WRITE REPORT TYPE TO XDATA(9):
C   FOR OCEANIC WIND SPEED           = 571
C   FOR OCEANIC PRECIPITABLE WATER   =  65
C   FOR RAINFALL RATE (LAND & OCEAN) =  66
C   FOR SURFACE TEMPERATURE          = 576
C   FOR OCEANIC CLOUD WATER          =  69
C   FOR SOIL MOISTURE                = 573
C   FOR SNOW DEPTH                   = 574
C   FOR SEA-SURFACE TEMPERATURE      = 577

         XDATA(9) = RPTYP(K)
      END IF

C***********************************************************************
C   COME HERE FOR ALL DATA TYPES (PRODUCTS OR BRIGHTNESS TEMPERATURES)
C***********************************************************************

C  STNID HOLDS STATION IDENTIFICATION
C    -- POSITION 1 INDICATES THIS IS A SUPEROB ................. 'S'
C    -- POSITION 8 HOLDS SSM/I DATA TYPE
C        OCEANIC WIND SPEED .................................... 'A'
C        OCEANIC PRECIPITABLE WATER ............................ 'E'
C        RAINFALL RATE ......................................... 'I'
C        SURFACE TEMPERATURE ................................... 'M'
C        OCEANIC CLOUD WATER ................................... 'Q'
C        SOIL MOISTURE ......................................... 'S'
C        SNOW DEPTH ............................................ 'U'
C        SEA-SURFACE TEMPERATURE ............................... 'Z'
C        SEVEN-CHANNEL BRIGHTNESS TEMPERATURES ................. 'Y'
C    -- POSITIONS 2-7 HOLD THE SEQUENTIAL SERIAL INDEX (000001 - 999999)
C        (UNIQUE FOR EACH DATA TYPE)

      STNID(1:1) = 'S'
      WRITE(STNID(2:7),'(I6.6)')  ICNT(K)
      STNID(8:8) = CPRD(K)


C  ENCODE HEADER INFORMATION (SAME FOR ALL TYPES) INTO SUBSET (REPORT)
C  -------------------------------------------------------------------

      HDR_8(2:15) = XDATA(1:14)
      CALL UFBINT(NFILE,HDR_8(1:11),11,1,IRET,
     $ 'RPID YEAR MNTH DAYS HOUR MINU SECO CLAT CLON NMCT ACAV')

      IF(K.LT.9)  THEN

C  ENCODE THE SINGLE RETRIEVAL PRODUCT (MEAN AND STANDARD DEVIATION)
C   DATA INTO SUBSET (REPORT) (THE PRODUCT IS BASED ON THE VALUE OF K)
C  -------------------------------------------------------------------

         PROD_8(1,1) =  4
         PROD_8(2,1) = XDATA(14+K)
         PROD_8(1,2) = 10
         PROD_8(2,2) = XDATA(22+K)
         CALL UFBINT(NFILE,PROD_8,2,1,IRET,CPROD(K))
         CALL UFBREP(NFILE,PROD_8,2,2,IRET,CPROD(K))

C  ENCODE METEOROLOGICAL FEATURE (FIRST AS 12, CLOUD; AGAIN AS
C   MISSING, CANCELLED), BUT ONLY FOR CLOUD WATER RPEORTS
C  -----------------------------------------------------------

         IF(K.EQ.5)  THEN
            METFET_8(1) = 12
            METFET_8(2) = BMISS
            METFET_8(3) = 12
            METFET_8(4) = BMISS
            CALL UFBREP(NFILE,METFET_8,1,4,IRET,'METFET')
         END IF

      ELSE  IF(K.EQ.10)  THEN

C  ENCODE THE RADIANCE DATA INTO SUBSET (REPORT)
C  ---------------------------------------------

         BTMP_8(1,1,1:7) =  4
         BTMP_8(2,1,1:7) = YDATA(2,1:7)
         BTMP_8(1,2,1:7) = 10
         BTMP_8(2,2,1:7) = YDATA(3,1:7)
         CALL UFBINT(NFILE,BTMP_8,2, 7,IRET,'FOST TMBR')
         CALL UFBREP(NFILE,BTMP_8,2,14,IRET,'FOST TMBR')
         CHNM_8 = YDATA(1,1:7)
         CALL UFBINT(NFILE,CHNM_8,1, 7,IRET,'CHNM')

      END IF

C  WRITE THE ENCODED SUBSET (REPORT) INTO THE OUTPUT BUFR MESSAGE
C  --------------------------------------------------------------

      CALL WRITSB(NFILE)

      CALL UFBCNT(NFILE,IREC,ISUB)
      IF(IREC.GT.IRECL)  THEN
         ISUBT = ISUBT + ISUBL
         WRITE(6,1254) IREC-1,ISUBL,ISUBT
 1254 FORMAT(/' --- THIS REPORT OPENS NEW OUTPUT BUFR MSG: LAST MSG ',
     $ 'WAS NO.',I10,' (DATA) WITH',I5,' RPTS (TOTAL NO. RPTS WRITTEN=',
     $ I7,')')
      END IF

      ISUBL = ISUB
      IRECL = IREC

      RETURN

      END

C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SUPOB1      SUPEROBS PRODUCTS,STORES IN XDATA ARRAY
C   PRGMMR: D. A. KEYSER     ORG: NP22       DATE: 1999-02-17
C
C ABSTRACT: CREATES SUPEROBS FROM INDIVIDUAL RETRIEVALS OF SPECIFIED
C   PRODUCT(S) WITHIN SPECIFIED LAT/LON BOXES.  SUPEROBS ARE FORMED
C   FROM LINEAR MEANS OF PRODUCT VALUE AND TIME AMONGST ALL RETRIEVALS
C   OF PRODUCT WITHIN LAT/LON GRID BOXES THAT CONTAIN AT LEAST ONE
C   RETRIEVAL.  SUPEROBS ARE LOCATED AT THE MEAN LATITUDE AND
C   LONGITUDE POSITION WITHIN THE BOX.  THE STANDARD DEVIATION IS
C   ALSO STORED.  EACH SUPEROB IS THEN STORED INTO XDATA ARRAY AND
C   SENT TO SUBROUTINE OBUFRS FOR ENCODING INTO OUTPUT BUFR FILE.
C   THIS SUBROUTINE HANDLES ONLY INPUT FROM THE PRODUCTS FILE OR THE
C   WIND SPEED PRODUCT CALCULATED IN-LINE VIA NEURAL NET OR GOODBERLET
C   ALGORITHM FROM INPUT BRIGHTNESS TEMP FILE OR THE TPW PRODUCT
C   CALCULATED IN-LINE VIA NEURAL NET ALGORITHM FROM THE INPUT
C   BRIGHTNESS TEMP FILE.  SEE SUBROUTINE SUPOB2 FOR THE DIRECT
C   PROCESSING OF BRIGHTNESS TEMPERATURES.
C
C PROGRAM HISTORY LOG:
C 1993-01-21  D. A. KEYSER (NMC22) -- ORIGINAL AUTHOR
C 1993-07-21  D. A. KEYSER (NMC22) -- FOR SUPEROBS, ADDED STORAGE OF
C        STANDARD DEVIATION (IN CHARACTER FORMAT IN BYTES 1-4 OF
C        RESERVE WORD 5 OF UNPACKED ON29 ARRAY) AND NUMBER OF INDIVIDUAL
C        RETRIEVALS THAT WENT IN TO GENERATING SUPEROB (IN CHARACTER
C        FORMAT IN BYTES 1 & 2 OF RESERVE WORD 6)
C 1994-04-12  D. A. KEYSER (NMC22) -- FOR SUPEROBS, ADDED NAMELIST
C        SWITCH 'LIMCNT' TO ALLOW USER TO CONTROL THE GENERATION OF
C        SUPEROBS BASED ON THE NUMBER OF INDIVIDUAL RETRIEVALS THAT
C        WOULD GO INTO MAKING THE SUPEROB.  MADE NECESSARY
C        MODIFICATIONS TO ALLOW SUBROUTINE TO COMPILE ON EITHER THE
C        NAS OR THE CRAY
C 1994-09-06  D. A. KEYSER (NMC22) -- IN ORDER TO GET REGION SIZE DOWN
C        BELOW NAS LIMIT OF 9999K BYTES, COMBINED LATITUDE AND
C        LONGITUDE IN NEW STORAGE ARRAY "IPKDTA" (REPLACED "SUMLAT"
C        AND "SUMLON" STORAGE ARRAYS), EACH HELD IN 16-BITS; MODIFIED
C        TO ALLOW SUPEROBING OVER .5 DEG. BOXES, BUT ONLY IN NORTHERN
C        AND WESTERN HEMISPHERES
C 1994-10-05  D. A. KEYSER (NMC22) --  ADDED THE PRODUCTS CLOUD WATER,
C        SOIL MOISTURE AND SNOW DEPTH TO THE LIST OF PRODUCTS THAT CAN
C        BE PROCESSED; RENAMED TO 'SUPOB1' FROM 'SUPROB'
C 1995-01-05  D. A. KEYSER (NMC22) -- ADDED ABILITY TO PROCESS AND
C        -IF REQUESTED- SUPEROB WIND SPEED PRODUCTS THAT HAVE BEEN
C        GENERATED IN-LINE (VIA SUBR. W3FI86) USING EITHER THE
C        NEURAL NETWORK ALGORITHM OR THE GOODBERLET ALGORITHM.
C        THE ORIGINAL GOODBERLET WIND SPEED IN THE PRODUCTS DATA
C        SET(S) CAN ALSO STILL BE PROCESSED; NOTE HOWEVER THAT
C        ONLY ONE SOURCE OF WIND SPEED PRODUCT CAN BE CHOSEN IN A
C        SINGLE RUNNING OF THIS PROGRAM
C 1996-07-31  D. A. KEYSER (NP22) -- CAN NOW PROCESS OPERATIONAL
C        FNOC PRODUCTS ALONG WITH IN-LINE CALCULATED WIND SPEED
C        PRODUCT.
C 1999-02-17  D.A. KEYSER -- STORES SUPEROBED DATA IN XDATA ARRAY
C        RATHER THAN UNPACKED ON29 ARRAY (OUTPUT FILE IS NOW IN BUFR)
C
C USAGE:    CALL SUPOB1
C
C   OUTPUT FILES:
C     UNIT 06  - STANDARD OUTPUT PRINT
C
C REMARKS: CALLED BY MAIN PROGRAM.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  WCOSS
C
C$$$
      SUBROUTINE SUPOB1

      INTEGER  NDATE(8)

      REAL  SCALE(8)

      COMMON/KOUNT/ICNT(10),ICNTT,NFILE,IRECL,ISUBL,ISUBT
      COMMON/SWITCH/IVAR(10),IQM,ISUPOB,LATS,LATN,LONW,LONE,LIMCNT,QMPW
      COMMON/BGRID/BLAT(360),BLON(720),NOBS(8,720,360)
      COMMON/SAVED/ITOBS(9),ITOBST,DGH,DGV,SUMDTA(8,720,360),
     $ SSQDTA(8,720,360),SUMTIM(8,720,360),SUMLAT(8,720,360),
     $ SUMLON(8,720,360)
      COMMON/DGRID/LATSIZ,LONSIZ
      COMMON/REPT/XDATA(36),YDATA(3,7)
      COMMON/CENTER/IDATE(8)

      DATA SCALE/ 10., 10.,1000000., 100., 100., 1000., 1000.,100./
      DATA BMISS/10E8/

C***********************************************************************
C            LOOP THRU THE PRODUCT TYPES AS SPECIFIED BY USER
C***********************************************************************

      LOOP1: DO K = 1,8

C  CHECK FOR USER-SPECIFICATION AND VALID PRODUCT
C  ----------------------------------------------

         IF(IVAR(K).NE.0)  CYCLE LOOP1

C-----------------------------------------------------------------------
C   LOOP THRU BOXES: CONSTRUCT MEANS & WRITE SUPEROB TO XDATA ARRAY
C-----------------------------------------------------------------------

         LATBEG = 1
         KNDX = 0
         LOOP1n1: DO KI = LATBEG,LATSIZ
            KNDX = KNDX + 1
            LOOP1n2: DO KJ = 1,LONSIZ

C  IF LESS THAN 'LIMCNT' INDIVIDUAL RETRIEVALS WENT INTO MAKING
C   SUPEROB IN THIS BOX, SKIP PROCESSING THE SUPEROB FOR THIS BOX
C  --------------------------------------------------------------

               IF(NOBS(K,KJ,KNDX).LT.LIMCNT)  CYCLE LOOP1n2
               XDATA = BMISS
               ICNT(K) = MIN(999999,ICNT(K)+1)
               ICNTT = ICNTT + 1
               ITOBS(K) = ITOBS(K) + NOBS(K,KJ,KNDX)
               ITOBST = ITOBST + NOBS(K,KJ,KNDX)
               XMUL = FLOAT(NOBS(K,KJ,KNDX))

C.......................................................................

C  CALC. MEAN AND STANDARD DEVIATION OF THE PRODUCT VALUE AND THE
C   MEAN OF THE TIME, LATITUDE AND LONGITUDE WITHIN EACH GRID BOX
C  --------------------------------------------------------------

               AVGDTA = SUMDTA(K,KJ,KNDX)/XMUL
               QUAN   = MAX((SSQDTA(K,KJ,KNDX)/XMUL - AVGDTA**2),0.)
               STDDTA = SQRT(QUAN)
               AVGTIM = SUMTIM(K,KJ,KNDX)/XMUL
               AVGLAT = SUMLAT(K,KJ,KNDX)/XMUL
               AVGLON = SUMLON(K,KJ,KNDX)/XMUL

C  STORE MEAN TIME IN "ITIME" (CHECK FOR MEAN TIME INTO THE NEXT DAY)
C  ------------------------------------------------------------------

               ITIME = NINT(AVGTIM + 0.00005)
               IF(ITIME.GE.24000)  ITIME = ITIME - 24000.

C  WRITE REPORT YEAR, MONTH, DAY TO XDATA(1), XDATA(2), XDATA(3), RESP.
C   MODIFY (IF NEEDED) CENTER DATE YEAR, MONTH AND DAY TO PROPERLY
C   DEFINE THE YEAR, MONTH AND DAY OF THE SUPEROB
C  --------------------------------------------------------------------

               NDATE = IDATE
               JTIME=NINT(1000. * (REAL(IDATE(5)) + REAL(IDATE(6))/60. +
     $          REAL(IDATE(7))/3600.))
               IF(ABS(JTIME-ITIME).GT.6000)  THEN
                  DAYCHG = SIGN(1.0,REAL(JTIME-ITIME))
                  CALL W3MOVDAT((/DAYCHG,0.,0.,0.,0./),IDATE,NDATE)
               END IF
               XDATA(1:3) = NDATE(1:3)

C  WRITE REPORT HOUR, MIN, SEC TO XDATA(4), XDATA(5), XDATA(6), RESP.
C  ------------------------------------------------------------------

               XDATA(4)  = INT(ITIME/1000.)
               XMIN      = (ITIME - (NINT(XDATA(4)) * 1000)) * 0.06
               XDATA(5)  = INT(XMIN)
               XDATA(6)  = (NINT(XMIN*100.) - NINT(XDATA(5))*100) * 0.6

C  WRITE LAT. TO XDATA(7): INPUT: N(+), S(-) X 100; OUTPUT: N(+), S(-)
C  -------------------------------------------------------------------

               XDATA(7)  = NINT(AVGLAT + SIGN(0.00005,AVGLAT))/100.

C  WRITE LON. TO XDATA(8): INPUT: 0-360 EAST X 100; OUTPUT: 0-360 EAST
C  -------------------------------------------------------------------

               XDATA(8)  = NINT(AVGLON + 0.00005)/100.

C  REPORT TYPE WILL BE WRITTEN TO XDATA(9) IN SUBR. OBUFRS (VARIES)
C  ----------------------------------------------------------------


C  WRITE # OF INDIVID. RETR. THAT WENT INTO MAKING SUPEROB TO XDATA(10)
C  --------------------------------------------------------------------

               XDATA(10)  = NOBS(K,KJ,KNDX)

C  VALUES IN XDATA(11) THROUGH XDATA(14) APPLY ONLY TO NON-SUPEROBS
C  ----------------------------------------------------------------


C  WRITE MEAN PROD VALUE TO XDATA(14+K) (SEE SUBR STORE1 FOR K & UNITS)
C  --------------------------------------------------------------------

               XDATA(14+K)  = NINT(AVGDTA + 0.00005)/SCALE(K)


C  WRITE STANDARD DEVIATION OF PRODUCT VALUE TO XDATA(22+K)
C   (SEE SUBR STORE1 FOR K & UNITS)
C  --------------------------------------------------------

               XDATA(22+K)  = NINT(STDDTA + 0.00005)/SCALE(K)


C  SUBROUTINE OBUFRS ENCODES THE SUPEROB INTO OUTPUT BUFR FILE
C  -----------------------------------------------------------

               CALL OBUFRS(K)

            ENDDO LOOP1n2
         ENDDO  LOOP1n1

C-----------------------------------------------------------------------

      ENDDO LOOP1

C***********************************************************************

C  ALL SUPEROB SCAN/RETRIEVALS HAVE BEEN PROCESSED
C  -----------------------------------------------

      RETURN

      END

C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SUPOB2      SUPEROBS B.TEMPS, STORES IN X/YDATA ARRAY
C   PRGMMR: D. A. KEYSER     ORG: NP22       DATE: 1999-02-17
C
C ABSTRACT: CREATES SUPEROBS FROM INDIVIDUAL RETRIEVALS CONTAINING
C   SEVEN CHANNELS OF BRIGHTNESS TEMPERATURES WITHIN SPECIFIED LAT/
C   LON BOXES.  SUPEROBS ARE FORMED FROM LINEAR MEANS OF THE SEVEN
C   BRIGHTNESS TEMPERATURES AND TIME AMONGST ALL RETRIEVALS WITHIN
C   LAT/LON GRID BOXES THAT CONTAIN AT LEAST ONE RETRIEVAL.  SUPEROBS
C   ARE LOCATED AT THE MEAN LATITUDE AND LONGITUDE POSITION WITHIN THE
C   BOX.  EACH SUPEROB IS THEN STORED INTO XDATA ARRAY AND SENT TO
C   SUBROUTINE OBUFRS FOR ENCODING INTO OUTPUT BUFR FILE.  THIS
C   SUBROUTINE HANDLES ONLY THE BRIGHTNESS TEMPERATURES.  SEE
C   SUBROUTINE SUPOB1 FOR PRODUCTS.
C
C PROGRAM HISTORY LOG:
C 1994-10-05  D. A. KEYSER (NMC22) --  ADAPTED FROM ORIGINAL
C        SUBROUTINE SUPROB, BUT NOW TO PROCESS THE SEVEN CHANNELS OF
C        BRIGHTNESS TEMPERATURES
C 1999-02-17  D.A. KEYSER -- STORES SUPEROBED DATA IN XDATA AND YDATA
C        ARRAYS RATHER THAN UNPACKED ON29 ARRAY (OUTPUT FILE IS NOW
C        IN BUFR)
C
C USAGE:    CALL SUPOB2
C   OUTPUT FILES:
C     UNIT 06  - STANDARD OUTPUT PRINT
C
C REMARKS: CALLED BY MAIN PROGRAM.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  WCOSS
C
C$$$
      SUBROUTINE SUPOB2

      INTEGER  NDATE(8)

      COMMON/KOUNT/ICNT(10),ICNTT,NFILE,IRECL,ISUBL,ISUBT
      COMMON/SWITCH/IVAR(10),IQM,ISUPOB,LATS,LATN,LONW,LONE,LIMCNT,QMPW
      COMMON/BGRID/BLAT(360),BLON(720),NOBS(8,720,360)
      COMMON/SAVED/ITOBS(9),ITOBST,DGH,DGV,SUMDTA(8,720,360),
     $ SSQDTA(8,720,360),SUMTIM(8,720,360),SUMLAT(8,720,360),
     $ SUMLON(8,720,360)
      COMMON/DGRID/LATSIZ,LONSIZ
      COMMON/REPT/XDATA(36),YDATA(3,7)
      COMMON/CENTER/IDATE(8)

      DATA  BMISS/10E8/

C***********************************************************************
C   LOOP THRU BOXES: CONSTRUCT MEANS & WRITE SUPEROB TO XDATA ARRAY
C***********************************************************************

      LATBEG = 1
      KNDX = 0
      LOOP1: DO KI = LATBEG,LATSIZ
         KNDX = KNDX + 1
         LOOP1n1: DO KJ = 1,LONSIZ

C  IF LESS THAN 'LIMCNT' INDIVIDUAL RETRIEVALS WENT INTO MAKING
C   SUPEROB IN THIS BOX, SKIP PROCESSING THE SUPEROB FOR THIS GRID BOX
C  -------------------------------------------------------------------

            IF(NOBS(1,KJ,KNDX).LT.LIMCNT)  CYCLE LOOP1n1
            XDATA = BMISS
            YDATA = BMISS
            ICNT(10) = MIN(999999,ICNT(10)+1)
            ICNTT = ICNTT + 1
            ITOBS(9) = ITOBS(9) + NOBS(1,KJ,KNDX)
            ITOBST = ITOBST + NOBS(1,KJ,KNDX)
            XMUL = FLOAT(NOBS(1,KJ,KNDX))

C-----------------------------------------------------------------------

C  CALCULATE MEAN TIME, LATITUDE AND LONGITUDE WITHIN THIS GRID BOX
C  ----------------------------------------------------------------

            AVGTIM = SUMTIM(1,KJ,KNDX)/XMUL
            AVGLAT = SUMLAT(1,KJ,KNDX)/XMUL
            AVGLON = SUMLON(1,KJ,KNDX)/XMUL

C  STORE MEAN TIME IN "ITIME" (CHECK FOR MEAN TIME INTO THE NEXT DAY)
C  ------------------------------------------------------------------

            ITIME = NINT(AVGTIM + 0.00005)
            IF(ITIME.GE.24000)  ITIME = ITIME - 24000.

C  WRITE REPORT YEAR, MONTH, DAY TO XDATA(1), XDATA(2), XDATA(3), RESP.
C   MODIFY (IF NEEDED) CENTER DATE YEAR, MONTH AND DAY TO PROPERLY
C   DEFINE THE YEAR, MONTH AND DAY OF THE SUPEROB
C  --------------------------------------------------------------------

            NDATE = IDATE
            JTIME = NINT(1000. * (REAL(IDATE(5)) + REAL(IDATE(6))/60. +
     $       REAL(IDATE(7))/3600.))
            IF(ABS(JTIME-ITIME).GT.6000)  THEN
               DAYCHG = SIGN(1.0,REAL(JTIME-ITIME))
               CALL W3MOVDAT((/DAYCHG,0.,0.,0.,0./),IDATE,NDATE)
            END IF
            XDATA(1:3) = NDATE(1:3)

C  WRITE REPORT HOUR, MIN, SEC TO XDATA(4), XDATA(5), XDATA(6), RESP.
C  ------------------------------------------------------------------

            XDATA(4)  = INT(ITIME/1000.)
            XMIN      = (ITIME - (NINT(XDATA(4)) * 1000)) * 0.06
            XDATA(5)  = INT(XMIN)
            XDATA(6)  = (NINT(XMIN*100.) - NINT(XDATA(5))*100) * 0.6

C  WRITE LAT. TO XDATA(7): INPUT: N(+), S(-) X 100; OUTPUT: N(+), S(-)
C  -------------------------------------------------------------------

            XDATA(7)  = NINT(AVGLAT + SIGN(0.00005,AVGLAT))/100.

C  WRITE LON. TO XDATA(8): INPUT: 0-360 EAST X 100; OUTPUT: 0-360 EAST
C  -------------------------------------------------------------------

            XDATA(8)  = NINT(AVGLON + 0.00005)/100.

C  WRITE REPORT TYPE TO XDATA(9) (SET = 68)
C  ----------------------------------------

            XDATA(9)  = 68.

C  WRITE # OF INDIVID. RETR. THAT WENT INTO MAKING SUPEROB TO XDATA(10)
C  --------------------------------------------------------------------

            XDATA(10)  = NOBS(1,KJ,KNDX)

C  VALUES IN XDATA(11) THROUGH XDATA(14) APPLY ONLY TO NON-SUPEROBS
C  ----------------------------------------------------------------


C-----------------------------------------------------------------------
C LOOP THROUGH THE SEVEN CHANNELS OF BRIGHTNESS TEMPERATURE & CALCULATE
C    EACH CHANNEL'S MEAN AND STANDARD DEVIATION WITHIN THIS GRID BOX
C-----------------------------------------------------------------------

            DO K = 1,7

               AVGDTA = SUMDTA(K,KJ,KNDX)/XMUL
               QUAN   = MAX((SSQDTA(K,KJ,KNDX)/XMUL - AVGDTA**2),0.)
               STDDTA = SQRT(QUAN)

C  WRITE THE CHANNEL TO YDATA(1,K)
C  -------------------------------

               YDATA(1,K) = K

C  WRITE THE MEAN B. TEMP TO YDATA(2,K)  WHERE K=1,7 IS DEFINED IN
C   SUBR. STORE2, UNITS : INPUT: DEG. KELVIN X 100; OUTPUT: DEG. KELVIN
C  --------------------------------------------------------------------

               YDATA(2,K) = NINT(AVGDTA + 0.00005)/100.

C  WRITE THE STD DEV. TEMP TO YDATA(3,K) WHERE K=1,7 IS DEFINED IN
C   SUBR. STORE2, UNITS : INPUT: DEG. KELVIN X 100; OUTPUT: DEG. KELVIN
C  --------------------------------------------------------------------

               YDATA(3,K) = NINT(STDDTA + 0.00005)/100.

            ENDDO

C-----------------------------------------------------------------------

C  SUBROUTINE OBUFRS ENCODES THE SUPEROB INTO OUTPUT BUFR FILE
C  -----------------------------------------------------------

            CALL OBUFRS(10)

         ENDDO LOOP1n1
      ENDDO LOOP1

C***********************************************************************

C  ALL SUPEROB SCAN/RETRIEVALS HAVE BEEN PROCESSED
C  -----------------------------------------------

      RETURN

      END

C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    FIRST       BLOCK DATA: INIT. COMMON BLOCK PARAMETERS
C   PRGMMR: D. A. KEYSER     ORG: NP22       DATE: 1993-01-21
C
C ABSTRACT: INITIALIZES COMMON BLOCK PARAMETERS IN COMMON BLOCKS
C   /KOUNT/ AND /SAVED/.
C
C PROGRAM HISTORY LOG:
C 1993-01-21  D. A. KEYSER (NMC22) -- ORIGINAL AUTHOR
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  WCOSS
C
C$$$
      BLOCK DATA FIRST

      COMMON/KOUNT/ICNT(10),ICNTT,NFILE,IRECL,ISUBL,ISUBT
      COMMON/SAVED/ITOBS(9),ITOBST,DGH,DGV,SUMDTA(8,720,360),
     $ SSQDTA(8,720,360),SUMTIM(8,720,360),SUMLAT(8,720,360),
     $ SUMLON(8,720,360)

      DATA  ICNT/10*0/,ICNTT/0/,ITOBS/9*0/,ITOBST/0/,NFILE/51/,IRECL/0/,
     $ ISUBL/0/,ISUBT/0/

      END
